*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*----------------------------------------------------------------*
000200*    THIS COPYBOOK SETS UP THE RATE AND SERVICE CHARGE REGION    *
000300*    KEYS.  THE DEFAULT REGION KEY VALUE IS SIX ZEROES AND       *
000400*    FOUR SPACES.                                                *
000500*                                                                *
000600*    THE SYSTEM WILL CONCATENATE IN THE REGION KEY UP TO THREE   *
000700*    FIELDS FOR A MAXIMUM OF TEN CHARACTERS.  ANY COMBINATION    *
000800*    EXCEEDING THE MAXIMUM IS TRUNCATED.                         *
000900*                                                                *
001000*    OTHER METHODS CAN BE USED TO BUILD THE REGION KEY.  FOR     *
001100*    EXAMPLE:                                                    *
001200*              IF  IM-CONTROL-2 EQUAL '002'                      *
001300*                  MOVE 'REGION1' TO WMS-RATE-REGION             *
001400*                  MOVE 'SVC A'   TO WMS-SVC-CHRG-REGION.        *
001500*              IF  IM-CONTROL-3 GREATER THAN '003'               *
001600*              AND IM-CONTROL-3 LESS THAN '050'                  *
001700*                  MOVE 'AREA5'   TO WMS-RATE-REGION.            *
001800*----------------------------------------------------------------*
001900     MOVE WS-CONTROL-2           TO WS-BCR-CTL2.
002000     MOVE WS-CONTROL-3           TO WS-BCR-CTL3.
002100     MOVE SPACES                 TO WS-REGION-SPACES.
002200     IF  WS-RT-REG-FLD1 EQUAL SPACES
002300         GO TO SET-RATE-REGION.
002400     MOVE SPACES                 TO WS-REGION-KEY.
002500     MOVE +1                     TO WS-KEY-START.
002600     MOVE +10                    TO WS-KEY-LENGTH.
002700     MOVE WS-RT-REG-FLD1         TO WS-FLD-NBR.
002800
002900 MOVE-CHAR.
003000     IF  WS-FLD-NBR NOT NUMERIC
003100         PERFORM MOVE-CTL-OFFSET THRU MOVE-CTL-EXIT
003200     ELSE
003300         MOVE DMT-START(WS-FLD-NBR9)  TO WS-MST-START
003400         MOVE DMT-LENGTH(WS-FLD-NBR9) TO WS-MST-LENGTH.
003500     IF  WS-MST-START GREATER THAN WS-MSTR-FIXED-PORTION
003600         GO TO MOVE-CHAR-EXIT.
003700     IF  WS-MST-LENGTH NOT GREATER THAN WS-KEY-LENGTH
003800         SUBTRACT WS-MST-LENGTH FROM WS-KEY-LENGTH
003900     ELSE
004000         IF  WS-KEY-LENGTH GREATER THAN ZERO
004100             MOVE WS-KEY-LENGTH  TO WS-MST-LENGTH
004200             MOVE ZERO           TO WS-KEY-LENGTH
004300         ELSE
004400             GO TO MOVE-CHAR-EXIT.
004500     PERFORM MOVE-LOOP         THRU MOVE-LOOP-EXIT
004600                                    WS-MST-LENGTH TIMES.
004700     GO TO MOVE-CHAR-EXIT.
004800
004900 MOVE-LOOP.
005000     MOVE MASTER-BYTE (WS-MST-START)
005100                                 TO WS-REGION-BYTE (WS-KEY-START).
005200     ADD +1                      TO WS-MST-START
005300                                    WS-KEY-START.
005400 MOVE-LOOP-EXIT.
005500     EXIT.
005600
005700 MOVE-CTL-OFFSET.
005800     IF  WS-FLD-NBR EQUAL 'CTL1'
005900         MOVE WS-CTL1-START      TO WS-MST-START
006000         MOVE WS-CTL1-LENGTH     TO WS-MST-LENGTH
006100         GO TO MOVE-CTL-EXIT.
006200     IF  WS-FLD-NBR EQUAL 'CTL2'
006300         MOVE WS-CTL2-START      TO WS-MST-START
006400         MOVE WS-CTL2-LENGTH     TO WS-MST-LENGTH
006500         GO TO MOVE-CTL-EXIT.
006600     IF  WS-FLD-NBR EQUAL 'CTL3'
006700         MOVE WS-CTL3-START      TO WS-MST-START
006800         MOVE WS-CTL3-LENGTH     TO WS-MST-LENGTH
006900         GO TO MOVE-CTL-EXIT.
007000     IF  WS-FLD-NBR EQUAL 'CTL4'
007100         MOVE WS-CTL4-START      TO WS-MST-START
007200         MOVE WS-CTL4-LENGTH     TO WS-MST-LENGTH
007300         GO TO MOVE-CTL-EXIT.
007400     MOVE +9999                  TO WS-MST-START.
007500 MOVE-CTL-EXIT.
007600     EXIT.
007700
007800 MOVE-CHAR-EXIT.
007900     EXIT.
008000
008100 CHK-RATE-REGION-FLD2.
008200     IF  WS-RT-REG-FLD2 EQUAL SPACES
008300         GO TO SET-RATE-REGION.
008400     MOVE WS-RT-REG-FLD2         TO WS-FLD-NBR.
008500     PERFORM MOVE-CHAR         THRU MOVE-CHAR-EXIT.
008600     IF  WS-RT-REG-FLD3 EQUAL SPACES
008700         GO TO SET-RATE-REGION.
008800     MOVE WS-RT-REG-FLD3         TO WS-FLD-NBR.
008900     PERFORM MOVE-CHAR         THRU MOVE-CHAR-EXIT.
009000
009100 SET-RATE-REGION.
009200     MOVE WS-REGION-KEY          TO WMS-RATE-REGION.
009300
009400 BUILD-SC-REGION.
009500     MOVE WS-CONTROL-2           TO WS-BCR-CTL2.
009600     MOVE WS-CONTROL-3           TO WS-BCR-CTL3.
009700     MOVE SPACES                 TO WS-REGION-SPACES.
009800     IF  WS-SC-REG-FLD1 EQUAL SPACES
009900         GO TO SET-SVCH-REGION.
010000     MOVE SPACES                 TO WS-REGION-KEY.
010100     MOVE +1                     TO WS-KEY-START.
010200     MOVE +10                    TO WS-KEY-LENGTH.
010300     MOVE WS-SC-REG-FLD1         TO WS-FLD-NBR.
010400     PERFORM MOVE-CHAR         THRU MOVE-CHAR-EXIT.
010500     IF  WS-SC-REG-FLD2 EQUAL SPACES
010600         GO TO SET-SVCH-REGION.
010700     MOVE WS-SC-REG-FLD2         TO WS-FLD-NBR.
010800     PERFORM MOVE-CHAR         THRU MOVE-CHAR-EXIT.
010900     IF  WS-SC-REG-FLD3 EQUAL SPACES
011000         GO TO SET-SVCH-REGION.
011100     MOVE WS-SC-REG-FLD3         TO WS-FLD-NBR.
011200     PERFORM MOVE-CHAR         THRU MOVE-CHAR-EXIT.
011300
011400 SET-SVCH-REGION.
011500     MOVE WS-REGION-KEY          TO WMS-SVC-CHRG-REGION.
011600*
