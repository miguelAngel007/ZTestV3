*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*
000200*
000300*--------------------------------------------------------------*
000400*    IMPACS EXTENDED FLOAT RECORD INITIALIZE COPYBOOK          *
000500*--------------------------------------------------------------*
000600*
000700*
000800 INIT-EXT-FLOAT.
000900     MOVE CLEAR-EXTENDED-FLOAT-REC
001000                                 TO WFF-EXTENDED-FLOAT-REC.
001100
001200*--------------------------------------------------------------*
001300*    BYPASS INITIALIZING EXTENDED FLOAT RECORDS IF             *
001400*    EXTENDED FLOAT OPTION IS NOT USED AT THE BCR LEVEL.       *
001500*    WHEN THE BCR FLAG IS OFF AND THE ACCOUNT HAS EXTENDED     *
001600*    FLOAT, THE FEATURE WAS TURNED OFF ON THE BCR TODAY.       *
001700*    TURN OFF THE ACCOUNT'S EXTEND FLOAT FLAG AS THE RECORDS   *
001800*    WILL BE DELETED.  AMOUNTS FROM THE EXTENDED FLOAT RECORD  *
001900*    WILL BE IN OCCURRENCE 12 OF THE CUSTOMER UNAVAILABLE      *
002000*    TRAILER.                                                  *
002100*--------------------------------------------------------------*
002200     IF  WBC-EXTEND-FLOAT-OPT EQUAL '0'
002300         IF  WMS-EXTEND-FLOAT EQUAL '0'
002400             GO TO INIT-EXT-FLOAT-EXIT
002500         ELSE
002600             MOVE '0'            TO WMS-EXTEND-FLOAT
002700             GO TO INIT-EXT-FLOAT-EXIT.
002800
002900*--------------------------------------------------------------*
003000*    BYPASS INITIALIZING EXTENDED FLOAT RECORDS IF             *
003100*    - ACCOUNT HAS NO CUSTOMER AVAILABILITY TRAILER            *
003200*    - EXTENDED FLOAT RECORD ALREADY EXISTS FOR ACCOUNT        *
003300*--------------------------------------------------------------*
003400     IF  WMS-EXTEND-FLOAT     EQUAL '1'
003500     OR  WMS-CUST-AVAIL-TRLR  EQUAL '0'
003600         GO TO INIT-EXT-FLOAT-EXIT.
003700
003800     IF  WMS-CONTROL-KEY      EQUAL FF-KEY
003900     AND WBC-EXTEND-FLOAT-OPT NOT EQUAL '2'
004000         GO TO INIT-EXT-FLOAT-EXIT.
004100
004200 INIT-EXT-FLOAT-BUILD.
004300*--------------------------------------------------------------*
004400*    CUSTOMER AVAILABILITY TRANSACTIONS (TRAN CODES 52 OR 53)  *
004500*    THAT RESTRICT FUNDS FOR MORE THAN 12 DAYS MAY CAUSE AN    *
004600*    EXTENDED FLOAT RECORD TO BE ADDED IF ONE DID NOT ALREADY  *
004700*    EXIST FOR THE ACCOUNT.                                    *
004800*--------------------------------------------------------------*
004900     MOVE 'N'                    TO AFF-FLAG.
005000     MOVE +0                     TO AFF-MAX-ENTRY.
005100     MOVE CLEAR-EXTENDED-FLOAT-REC
005200                                 TO WFF-EXTENDED-FLOAT-REC.
005300     MOVE WMS-CONTROL-KEY        TO WFF-KEY.
005400     MOVE ZERO                   TO WFF-ENTRIES.
005500     MOVE WMS-CUST-AVAIL-TRAILER TO WFF-FLOAT-01-12.
005600     MOVE AFF-FIXED              TO WFF-LENGTH.
005700
005800     MOVE +1                     TO X.
005900
006000 INIT-EXT-FLOAT-LOOP.
006100*--------------------------------------------------------------*
006200*    CUSTOMER AVAILABILITY TRANSACTIONS (TRAN CODES 52 OR 53)  *
006300*    THAT RESTRICT FUNDS FOR LESS THAN 12 DAYS THAT WERE       *
006400*    ENTERED PRIOR TO THE TRANSACTION CAUSING THE EXTENDED     *
006500*    FLOAT TRAILER TO BE ADDED UPDATED THE WORK-BCA-52-AMT     *
006600*    FIELDS AND NOT THE WMS-CUST-AVAIL-TRAILER AND MUST BE     *
006700*    ADDED TO THE WFF-FLOAT-AMT FIELDS.                        *
006800*--------------------------------------------------------------*
006900     ADD WORK-BCA-52-AMT (X)     TO WFF-FLOAT-AMT (X).
007000     ADD +1                      TO X
007100     IF  X                LESS THAN +13
007200         GO TO INIT-EXT-FLOAT-LOOP.
007300
007400     MOVE +1                     TO X.
007500
007600 INIT-EXT-FLOAT-EXIT.
007700     EXIT.
007800
