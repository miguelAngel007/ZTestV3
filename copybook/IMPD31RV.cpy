*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*----------------------------------------------------------------*
000200*    THIS COPYBOOK IS USED FOR RECOVERING LOAN PAYMENT INFO      *
000300*    INFORMATION WHEN A PAYMENT REVERSAL IS MADE.  IT PERFORMS   *
000400*    THE FOLLOWING FUNCTIONS:                                    *
000500*    > ADDS THE REVERSAL AMOUNT TO THE PAYMENT DUE AMOUNT        *
000600*    > LOCATES THE LAST ZERO AMOUNT BILLING TRAILER AND ADDS     *
000700*      THE REVERSAL AMOUNT TO THE BILLING TRAILER AMOUNT         *
000800*    > IF THE LAST TRAILER DOES NOT HAVE A PAYMENT DUE DATE,     *
000900*      A NEW BILLING DATE IS CALCULATED BY SUBTRACTING ONE       *
001000*      MONTH FROM THE PREVIOUS TRAILER'S DATE AND NO CHARGES     *
001100*      ARE ASSESSED                                              *
001200*    > IF NO BILLING TRAILERS EXIST, A NEW ONE IS CREATED USING  *
001300*      THE DATE OF THE ORIGINAL LOAN TRANSACTION; CHARGES MAY    *
001400*      OR MAY NOT BE ASSESSED                                    *
001500*    > ADDS THE REVERSAL AMOUNT TO THE PAST DUE AMOUNT IF THE    *
001600*      PAYMENT IS NOW PAST DUE OR DELINQUENT                     *
001700*    > RECALCULATES THE STATUS OF THE BILLING TRAILER AND        *
001800*      COMPARES IT TO THE TRAILER'S ORIGINAL STATUS              *
001900*    > IF THE ORIGINAL STATUS IS LESS THAN THE CALCULATED        *
002000*      STATUS, IM31 WILL RECALCULATE THE STATUS, GENERATE        *
002100*      CHARGES IN R0500, AND UPDATE PAST DUE COUNTERS            *
002200*    > IF THE ORIGINAL STATUS IS EQUAL TO OR GREATER THAN THE    *
002300*      CALCULATED STATUS, THE CALCULATED STATUS REPLACES THE     *
002400*      ORIGINAL AND NO FURTHER ACTION IS TAKEN                   *
002500*----------------------------------------------------------------*
002600 K1910-REV.
002700     MOVE +0 TO C-NO-DAYS.
002800     MOVE TR-LOAN-ORIG-DATE TO DT-LOW-DATE.
002900     CALL 'SIEDITDT' USING DATE-AREA
003000     IF  DATE-FLAG EQUAL '1'
003100         GO TO K1940.
003200     IF  WMS-NB-DC-ABM EQUAL TO '1'
003300         GO TO K1940.
003400     IF  (WMS-SAVINGS-TRLR EQUAL '1'
003500     AND WMS-CMA-INDICATOR EQUAL '1')
003600         GO TO K1940.
003700     ADD TR-AMOUNT TO WMS-PYMT-DUE-AMT.
003800     MOVE +0 TO Z.
003900
004000 K1915.
004100     ADD +1 TO Z.
004200     IF  Z GREATER THAN +6
004300         GO TO K1920.
004400     IF  WMS-BILL-DUE-AMT (Z) GREATER THAN ZERO
004500         GO TO K1915.
004600
004700 K1920.
004710     MOVE 'F' TO NEW-BILL-TRLR.                                   0827790
004800     ADD TR-AMOUNT TO WMS-BILL-DUE-AMT (Z).
004900     IF  WMS-BILL-DUE-DATE (Z) EQUAL SPACES
005000     AND Z GREATER THAN +1
005100         MOVE +1 TO C-NO-DAYS
005200         CALL 'SIDCMLO' USING WMS-BILL-DUE-DATE (Z)
005300                              WMS-BILL-DUE-DATE (Z - 1)
005400                              C-NO-DAYS.
005500     IF  WMS-BILL-DUE-DATE (Z) EQUAL SPACES
005600     AND Z EQUAL +1
005610         MOVE 'N'                TO NEW-BILL-TRLR                 0827790
005700         MOVE TR-LN-ORIG-DA      TO WMS-BILL-DUE-DA (Z)
005800         MOVE TR-LN-ORIG-MO      TO WMS-BILL-DUE-MO (Z)
005900         MOVE TR-LN-ORIG-YR      TO WMS-BILL-DUE-YR (Z)
006000         IF  WMS-BILL-DUE-YR (Z) LESS THAN WBC-NEXT-CENT-YR       0266741
006100             MOVE '20'           TO  WMS-BILL-DUE-CENT (Z)
006200         ELSE
006300             MOVE '19'           TO  WMS-BILL-DUE-CENT (Z).
006400     MOVE WMS-BILL-DUE-STAT (Z)  TO HOLD-BILL-DUE-STAT.
006500     MOVE ZERO                   TO WMS-BILL-DUE-STAT (Z).
006600     MOVE '11'                   TO SICOMPDT-DATE-FORMATS.
006700     MOVE WMS-BILL-DUE-MO (Z)    TO DT-L-MO.
006800     MOVE WMS-BILL-DUE-DA (Z)    TO DT-L-DA.
006900     MOVE WMS-BILL-DUE-YR (Z)    TO DT-L-YR.
007000     MOVE WBC-CAPTURE-DATE       TO DT-HIGH-DATE.                 0266741
007100     CALL 'SICOMPDT'          USING DT-LOW-DATE
007200                                    DT-HIGH-DATE
007300                                    SICOMPDT-RETURN-FLAG
007400                                    SICOMPDT-DATE-FORMATS.
007500     IF  SICOMPDT-RETURN-FLAG NOT EQUAL 'L'
007600         GO TO K1940.
007610     IF  NEW-BILL-TRLR EQUAL 'F'                                  0827790
007700         ADD WMS-BILL-DUE-AMT (Z)   TO WMS-PAST-DUE-AMT.          0827790
007800     CALL 'SIDIF1'            USING DATE-AREA.
007900     MOVE RET-DAYS               TO CURR-PAST-DUE-DAYS.
008000     MOVE WBC-LAST-PROC-THRU-DATE TO DT-HIGH-DATE.                0266741
008100     CALL 'SIDIF1'            USING DATE-AREA.
008200     MOVE RET-DAYS               TO PREV-PAST-DUE-DAYS.
008300     ADD +1                      TO CURR-PAST-DUE-DAYS
008400                                    PREV-PAST-DUE-DAYS.
008500
008600 K1930.
008700     IF  CURR-PAST-DUE-DAYS NOT GREATER THAN WMS-PAST-DUE-DAYS
008800         MOVE '2' TO WMS-BILL-DUE-STAT (Z).
008900     IF  CURR-PAST-DUE-DAYS GREATER THAN WMS-PAST-DUE-DAYS
009000         MOVE '4' TO WMS-BILL-DUE-STAT (Z).
009100     IF  HOLD-BILL-DUE-STAT LESS THAN WMS-BILL-DUE-STAT (Z)
009200     AND C-NO-DAYS EQUAL ZERO
009300         MOVE HOLD-BILL-DUE-STAT TO WMS-BILL-DUE-STAT (Z).
009400
009500 K1940.
009600     GO TO K1520.
