*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*--------------------------------------------------------------*
000200*    CHANGE THE LINK INDICATOR AFTER ALL MAINTENANCE           *
000300*--------------------------------------------------------------*
000400 K5900-XINV.
000500*
000600***  CREATE LINKED TODAY REPORT EXCEPTION QA
000700***  ADD TO SYSTEM AND GL TOTALS FOR ACCOUNTS LINKED TODAY
000800***  ACCOUNTS LINKED TODAY CHANGE FROM "A" OR "I", TO "L"
000900*
001000     IF  WXI-LINK-IND-OUT     EQUAL  'L'
001100         MOVE 'QA'               TO  IMEX-CODE-1
001200         PERFORM K5950-EXC     THRU  K5950-EXIT
001300         MOVE WXI-LINK-IND-OUT   TO  WMS-XINV-LINK-IND
001400         IF  WMS-XINV-PROCESS-IND EQUAL '1'
001500             MOVE '1'            TO  WXI-SAME-DAY
001600             ADD  +1             TO  T3-XINV-SD-IN-NO
001700                                     WS-GL-XINV-SD-IN-NO
001800         ELSE
001900         IF  WMS-XINV-PROCESS-IND EQUAL '2'
002000             MOVE '1'            TO  WXI-NEXT-DAY
002100             ADD  +1             TO  T3-XINV-ND-IN-NO
002200                                     WS-GL-XINV-ND-IN-NO.
002300*
002400***  CREATE DELINKED TODAY REPORT EXCEPTION QD
002500***  ADD TO SYSTEM AND GL TOTALS FOR ACCOUNTS DELINKED TODAY
002600***  ACCOUNTS DELINKED TODAY CHANGE FROM "D" TO SPACE
002700*
002800     IF  WMS-XINV-LINK-IND    EQUAL  'D'
002900         MOVE 'QD'               TO  IMEX-CODE-1
003000         MOVE WMS-XINV-LINK-IND  TO  WXI-LINK-IND-OUT
003100         PERFORM K5950-EXC     THRU  K5950-EXIT
003200         IF  WMS-XINV-PROCESS-IND EQUAL '1'
003300         AND WXI-LINK-IND-IN      EQUAL 'L'
003400             MOVE '0'            TO  WXI-SAME-DAY
003500             ADD  +1             TO  T3-XINV-SD-OUT-NO
003600                                     WS-GL-XINV-SD-OUT-NO
003700         ELSE
003800         IF  WMS-XINV-PROCESS-IND EQUAL '2'
003900         AND WXI-LINK-IND-IN      EQUAL 'L'
004000             MOVE '0'            TO  WXI-NEXT-DAY
004100             ADD  +1             TO  T3-XINV-ND-OUT-NO
004200                                     WS-GL-XINV-ND-OUT-NO.
004300*
004400***  ADD TO SYSTEM AND GL TOTALS NEW BANK POSITION ACCOUNTS
004500*
004600     IF  WMS-XINV-LINK-IND    EQUAL  'P'
004700         IF  WMS-XINV-PROCESS-IND EQUAL '1'
004800             MOVE '1'            TO  WXI-SAME-DAY
004900             ADD  +1             TO  T3-XINV-SD-IN-NO
005000                                     WS-GL-XINV-SD-IN-NO.
005100*
005200 K5900-EXIT.
005300     EXIT.
005400*
005500*--------------------------------------------------------------*
005600*    FOR SAME DAY EXTERNAL INVESTMENTS                         *
005700*    ENSURE THAT CALCULATED EXTERNAL INVESTMENT BALANCE        *
005800*    EQUALS MAINTAINED VALUE OF EXTERNAL INVESTMENT BALANCE    *
005900*--------------------------------------------------------------*
006000 K5910-XINV-BAL.
006100     IF  WMS-XINV-PROCESS-IND EQUAL  '0' OR '2'
006200         GO TO K5910-EXIT.
006300*
006400     IF  WXI-XINV-BALANCE     EQUAL  WMS-XINV-BALANCE
006500         GO TO K5910-EXIT.
006600*
006700***  CREATE INTERNAL ADJUSTMENT FOR DIFFERENCE
006800*
006900     SUBTRACT WXI-XINV-BALANCE FROM  WMS-XINV-BALANCE
007000                             GIVING  WK-HOLD-AMT13.
007100     ADD  WK-HOLD-AMT13          TO  T3-XINV-SD-INT-ADJ-AMT
007200                                     WS-GL-XINV-SD-INT-ADJ-AMT.
007300     MOVE 'QO'                   TO  IMEX-CODE-1.
007400     MOVE '02'                   TO  IMEX-REC-NO.
007500     MOVE WK-HOLD-AMT13          TO  EX02-BAL.
007600     ADD  +1                     TO  SEQ-CTR.
007700     MOVE SEQ-CTR                TO  EX02-SEQ-NO.
007800     PERFORM WRITE-EX-MST      THRU  WRITE-EX-EXIT.
007900*
008000 K5910-EXIT.
008100     EXIT.
008200*
008300*--------------------------------------------------------------*
008400*    QA   - ACCOUNT LINKED TODAY   (LINK = "L")                *
008500*    QD   - ACCOUNT DELINKED TODAY (LINK = "D")                *
008600*--------------------------------------------------------------*
008700 K5950-EXC.
008800     MOVE '15'                   TO  IMEX-REC-NO.
008900*
009000***  OVERLAY THE OLD AND NEW DATA AT MAINTENANCE TIME WITH
009100***  THE ORIGINAL AND FINAL VALUES OF THE LINK INDICATOR
009200*
009300     MOVE WXI-LINK-IND-OUT       TO  WXI-FM-NEW.
009400     MOVE WXI-LINK-IND-IN        TO  WXI-FM-OLD
009500                                     EX15-MST-OLD-DATA.
009600     MOVE WXI-FILE-MAINT         TO  EX15-FM-TRAN.
009700     PERFORM WRITE-EX-MST      THRU  WRITE-EX-EXIT.
009800*
009900 K5950-EXIT.
010000     EXIT.
