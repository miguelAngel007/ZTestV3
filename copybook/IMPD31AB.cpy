*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*----------------------------------------------------------------*
000200*    AUTOBORROW PROCESSING                                       *
000300*----------------------------------------------------------------*
000400*    USE COLLECTED BALANCE FOR BEGINNING BALANCE INSTEAD OF      *
000500*    DDA BALANCE.                                                *
000600*----------------------------------------------------------------*
000700 R3696.
000800     IF  WMS-NB-DC-ABM NOT EQUAL '1'
000900         GO TO R3700.
001000     MOVE WMS-CURR-BAL TO CUR-BAL.
001100     MOVE WMS-COLL-CALC TO CALC-FLAG.
001200     PERFORM K0104 THRU K0105.
001300     MOVE CUR-BAL TO COLL-BAL.
001400     PERFORM K0106 THRU K0106-EXIT.
001500     MOVE SPACES TO IM-ACM-ABM-TRAN.
001600     IF  WMS-NB-ABM-REP-TARG EQUAL ZERO
001700     AND WMS-NB-ABM-REP-INC EQUAL ZERO
001800     AND WMS-NB-ABM-BRW-TARG EQUAL ZERO
001900     AND WMS-NB-ABM-BRW-INC EQUAL ZERO
002000         PERFORM R3698 THRU R3698-X
002100         GO TO R3700.
002200     IF  WMS-NB-ABM-REP-INC EQUAL ZERO
002300         PERFORM R3698 THRU R3698-X
002400         GO TO R3700.
002500     IF  WMS-PRIN-BAL NOT GREATER THAN +0.00
002600         GO TO R3697-BOROW.
002700     IF  COLL-BAL NOT GREATER THAN WMS-NB-ABM-REP-TARG
002800         GO TO R3697-BOROW.
002900     SUBTRACT WMS-NB-ABM-REP-TARG FROM COLL-BAL.
003000     IF  WMS-PRIN-BAL NOT GREATER THAN COLL-BAL
003100         MOVE WMS-PRIN-BAL TO NB-REPAY-AMT
003200         GO TO R3696-REPAY.
003300     DIVIDE COLL-BAL BY WMS-NB-ABM-REP-INC GIVING NB-REPAY-CNT.
003400     MULTIPLY WMS-NB-ABM-REP-INC BY NB-REPAY-CNT
003500                                 GIVING NB-REPAY-AMT.
003600     IF  NB-REPAY-AMT NOT GREATER THAN +0.00
003700         PERFORM R3698 THRU R3698-X
003800         GO TO R3700.
003900 R3696-REPAY.
004000     MOVE 'C'              TO IM-ACD-TRAN-CODE.
004100     COMPUTE IM-ACD-DDA-SAV-BAL = COLL-BAL +
004200                                  WMS-NB-ABM-REP-TARG.
004300     SUBTRACT NB-REPAY-AMT FROM WMS-DDA-BAL.
004400     SUBTRACT NB-REPAY-AMT FROM WMS-CURR-BAL.
004500     SUBTRACT NB-REPAY-AMT FROM WMS-PRIN-BAL.
004600     SUBTRACT NB-REPAY-AMT FROM CUST-BAL.
004700     SUBTRACT NB-REPAY-AMT FROM CUR-BAL.
004800     ADD NB-REPAY-AMT      TO WMS-AVAIL-BAL.
004900     SUBTRACT NB-REPAY-AMT FROM COLL-BAL.
005000     ADD WMS-NB-ABM-REP-TARG TO COLL-BAL.
005100     MOVE NB-REPAY-AMT     TO IM-ACD-TRNSF-AMT.
005200     MOVE NB-REPAY-AMT     TO WK-HOLD-AMT11.
005300     MOVE WBC-GENERATED-TRANS (10) TO GEN-TRAN-OPTION-HOLD.       0266741
005400     MOVE WK-HOLD-AMT11 TO HOLD-AMT11.
005500     MOVE +0 TO PRIN-DISB.
005600     IF  HOLD-AMT11 GREATER THAN +0.00
005700         ADD HOLD-AMT11 TO LOAN-PRIN-PAID, PRIN-DISB,
005800                           WS-GL-LOAN-PRIN-PAID.
005900     ADD +1 TO FS-DB.
006000     MOVE '10' TO EX01-INT-CODE.
006100     MOVE 'G5' TO IMEX-CODE-2.
006200     MOVE '5' TO EX01-TYPE.
006300     MOVE WK-HOLD-AMT11 TO HOLD-AMT.
006400     MOVE EX-LOAN-DISB TO EX01-LOAN-DISB.
006500     MOVE ZERO TO EX01-NUMBER.
006600     PERFORM WRITE-GEN-TRANS THRU WRITE-GEN-EXIT.
006700     ADD +1 TO LOAN-AUTO-PMT-NO, WS-GL-LOAN-AUTO-PMT-NO.
006800     ADD WK-HOLD-AMT11 TO LOAN-AUTO-PMT-AMT
006900                          WS-GL-LOAN-AUTO-PMT-AMT.
007000     ADD +1 TO LOAN-CR-NO, WS-GL-LOAN-CR-NO, WMS-LN-STMT-CR-NO.
007100     ADD WK-HOLD-AMT11 TO LOAN-CR-AMT, WMS-LN-STMT-CR-AMT,
007200                          WS-GL-LOAN-CR-AMT.
007300     ADD PRIN-DISB TO LOAN-CR-PRIN, WS-GL-LOAN-CR-PRIN.
007400     MOVE +0 TO INT-DISB.
007500     ADD INT-DISB  TO LOAN-CR-INT, WS-GL-LOAN-CR-INT.
007600     MOVE +0 TO OTH-DISB.
007700     ADD OTH-DISB  TO LOAN-CR-OTH, WS-GL-LOAN-CR-OTH.
007800     MOVE '1' TO LOAN-POST-TODAY.
007900     MOVE WK-HOLD-AMT11 TO WMS-LAST-PYMT-AMT.
008000     MOVE WBC-CAPTURE-DATE TO WMS-LAST-PMT-DATE                   0266741
008100                             WMS-DATE-LAST-LOAN-ACTIVITY.
008200     PERFORM K7500 THRU K8500.
008300     ADD +1 TO DDA-LOAN-PMT-NO, WMS-SC-AMTTRNSFR-NO,
008400                                WS-GL-DDA-LOAN-PMT-NO.
008500     ADD WK-HOLD-AMT11 TO DDA-LOAN-PMT-AMT
008600                          WS-GL-DDA-LOAN-PMT-AMT.
008700     MOVE WK-HOLD-AMT11          TO  AMT-PD.
008800     ADD +1 TO WMS-STMT-NO-DEBITS.
008900     ADD WK-HOLD-AMT11 TO WMS-STMT-DR-AMT.
009000     IF WMS-IOD-RATE-PTR GREATER THAN ZERO
009100     OR  WMS-HIFI-INDICATOR GREATER THAN ZERO
009200         ADD +1 TO IOD-DB-NO, WS-GL-IOD-DB-NO
009300         ADD WK-HOLD-AMT11 TO IOD-DB-AMT, WS-GL-IOD-DB-AMT
009400     ELSE
009500         ADD +1 TO DDA-DB-NO, WS-GL-DDA-DB-NO
009600         ADD WK-HOLD-AMT11 TO DDA-DB-AMT, WS-GL-DDA-DB-AMT.
009700     MOVE WBC-GENERATED-TRANS (9) TO GEN-TRAN-OPTION-HOLD.        0266741
009800     MOVE '09' TO EX01-INT-CODE.
009900     MOVE 'G5' TO IMEX-CODE-2.
010000     MOVE '4' TO EX01-TYPE.
010100     MOVE ZERO TO EX01-NUMBER.
010200     MOVE WK-HOLD-AMT11 TO HOLD-AMT.
010300     PERFORM WRITE-GEN-TRANS THRU WRITE-GEN-EXIT.
010400     MOVE '1' TO POST-TODAY.
010500     MOVE HOLD-AMT TO WMS-AMT-LAST-MONETARY-ACTIVITY.
010600     MOVE WBC-CAPTURE-DATE TO WMS-DATE-LAST-MONETARY-ACT.         0266741
010700 R3696-BULD-IMACMT.
010800     MOVE WMS-CONTROL-2    TO IM-ACD-BANK-ID.
010900     MOVE WMS-ACCT-NO      TO IM-ACD-ACCT-NO.
011000     MOVE '00000000'       TO IM-ACD-COST-CENT.
011100     MOVE WBC-CAPTURE-MO   TO NB-ACD-DT-MM.                       0266741
011200     MOVE WBC-CAPTURE-DA   TO NB-ACD-DT-DD.                       0266741
011300     MOVE WBC-CAPTURE-YR   TO NB-ACD-DT-YY.                       0266741
011400     MOVE NB-ACD-DATE      TO IM-ACD-POST-DATE.
011500     MOVE COLL-BAL         TO IM-ACD-DDA-BAL.
011600     MOVE 904              TO IM-ACD-FUND-NUMB.
011700     MOVE WBC-MONTH-END    TO IM-ACD-MTH-END-IND.                 0266741
011800     MOVE 'O'              TO I-O-CONTROL-ACCESS.
011900     MOVE 'L'              TO I-O-CONTROL-OPERATOR.
012000     CALL 'IMACDTS'        USING I-O-CONTROL-AREA
012100                                 IM-ACM-ABM-MONETARY.
012200     PERFORM R3698 THRU R3698-X.
012300 R3696-GOTO.
012400     GO TO R3700.
012500 R3697-BOROW.
012600     IF  WMS-NB-ABM-BRW-INC EQUAL 0
012700         PERFORM R3698 THRU R3698-X
012800         GO TO R3700.
012900     IF  WMS-NB-ABM-BRW-TARG EQUAL TO +9999999
013000         PERFORM R3698 THRU R3698-X
013100         GO TO R3700.
013200     IF  COLL-BAL NOT LESS THAN WMS-NB-ABM-BRW-TARG
013300         PERFORM R3698 THRU R3698-X
013400         GO TO R3700.
013500     IF  WMS-AVAIL-BAL LESS THAN WMS-NB-ABM-BRW-INC
013600         PERFORM R3698 THRU R3698-X
013700         GO TO R3700.
013800     SUBTRACT COLL-BAL FROM WMS-NB-ABM-BRW-TARG GIVING
013900                            NB-WRK-BRW.
014000     DIVIDE NB-WRK-BRW BY WMS-NB-ABM-BRW-INC GIVING
014100                            NB-BRW-CNT.
014200     MULTIPLY NB-BRW-CNT BY WMS-NB-ABM-BRW-INC GIVING NB-BRW-AMT.
014300     IF  COLL-BAL + NB-BRW-AMT LESS THAN WMS-NB-ABM-BRW-TARG
014400         ADD +1 TO NB-BRW-CNT
014500         ADD WMS-NB-ABM-BRW-INC TO NB-BRW-AMT.
014600     IF  NB-BRW-AMT GREATER THAN WMS-AVAIL-BAL
014700         MOVE WMS-AVAIL-BAL TO NB-BRW-AMT.
014800     IF  NB-BRW-AMT NOT GREATER THAN +0.00
014900         PERFORM R3698 THRU R3698-X
015000         GO TO R3700.
015100     MOVE 'D'              TO IM-ACD-TRAN-CODE.
015200     MOVE COLL-BAL         TO IM-ACD-DDA-SAV-BAL.
015300     ADD NB-BRW-AMT        TO WMS-DDA-BAL.
015400     ADD NB-BRW-AMT        TO WMS-CURR-BAL.
015500     ADD NB-BRW-AMT        TO WMS-PRIN-BAL.
015600     SUBTRACT NB-BRW-AMT   FROM WMS-AVAIL-BAL.
015700     ADD NB-BRW-AMT        TO COLL-BAL.
015800     MOVE NB-BRW-AMT       TO IM-ACD-TRNSF-AMT.
015900     MOVE 'D'              TO IM-ACD-TRAN-CODE.
016000     PERFORM R3696-BULD-IMACMT.
016100     MOVE WBC-CAPTURE-DATE TO WMS-DATE-LAST-BAL-CHANGE.           0266741
016200     MOVE NB-BRW-AMT TO HOLD-AMT.
016300     ADD +1 TO FS-CR.
016400     PERFORM K7500 THRU K8500.
016500     MOVE WBC-GENERATED-TRANS (7) TO GEN-TRAN-OPTION-HOLD.        0266741
016600     ADD +1 TO DDA-LOAN-NO, WS-GL-DDA-LOAN-NO.
016700     ADD HOLD-AMT TO DDA-LOAN-AMT, WS-GL-DDA-LOAN-AMT.
016800     ADD +1 TO WMS-STMT-NO-CREDITS.
016900     ADD HOLD-AMT TO WMS-STMT-CR-AMT.
017000     IF  WMS-IOD-RATE-PTR GREATER THAN ZERO
017100     OR  WMS-HIFI-INDICATOR GREATER THAN ZERO
017200         ADD +1 TO IOD-CR-NO, WS-GL-IOD-CR-NO
017300         ADD HOLD-AMT TO IOD-CR-AMT, WS-GL-IOD-CR-AMT
017400     ELSE
017500         ADD +1 TO DDA-CR-NO, WS-GL-DDA-CR-NO
017600         ADD HOLD-AMT TO DDA-CR-AMT, WS-GL-DDA-CR-AMT.
017700     MOVE '07' TO EX01-INT-CODE.
017800     MOVE '2' TO EX01-TYPE.
017900     MOVE ZERO TO EX01-NUMBER.
018000     PERFORM WRITE-GEN-TRANS THRU WRITE-GEN-EXIT.
018100     MOVE WBC-GENERATED-TRANS (8) TO GEN-TRAN-OPTION-HOLD.        0266741
018200     ADD +1 TO LOAN-TODAY-NO, WS-GL-LOAN-TODAY-NO.
018300     ADD HOLD-AMT TO LOAN-TODAY-AMT, WS-GL-LOAN-TODAY-AMT.
018400     ADD +1 TO LOAN-DB-NO, WS-GL-LOAN-DB-NO, WMS-LN-STMT-DB-NO.
018500     ADD HOLD-AMT TO LOAN-DB-AMT, WS-GL-LOAN-DB-AMT
018600                     WMS-LN-STMT-DB-AMT
018700                     LOAN-DR-PRIN, WS-GL-LOAN-DR-PRIN.
018800     MOVE '08' TO EX01-INT-CODE.
018900     MOVE 'G5' TO IMEX-CODE-2.
019000     MOVE '6' TO EX01-TYPE.
019100     PERFORM R0345 THRU R0346.
019200     MOVE HOLD-AMT TO PRIN-DISB.
019300     MOVE EX-LOAN-DISB TO EX01-LOAN-DISB.
019400     MOVE ZERO TO EX01-NUMBER.
019500     PERFORM WRITE-GEN-TRANS THRU WRITE-GEN-EXIT.
019600     MOVE WBC-CAPTURE-DATE TO WMS-DATE-LAST-LOAN-ACTIVITY         0266741
019700                             WMS-DATE-LAST-ADVANCE.
019800     MOVE HOLD-AMT TO WMS-AMT-LAST-ADVANCE.
019900     MOVE ZERO TO LOAN-TODAY.
020000     GO TO R3700.
020100 R3698.
020200     MOVE WMS-CONTROL-2    TO IM-ACF-BANK-ID.
020300     MOVE ZEROS            TO IM-ACF-ACCOUNT IM-ACF-COST-CENT.
020400     MOVE WMS-ACCT-NO      TO IM-ACF-ACCT-NO.
020500     MOVE '00000000'       TO IM-ACF-COST-CENT.
020600     MOVE ZEROS            TO IM-ACF-BRCH-NO.
020700     MOVE WMS-STATUS       TO IM-ACF-ACCT-STATUS.
020800     MOVE WMS-OFFICER      TO IM-ACF-OFFICER.
020900     MOVE '  '             TO IM-ACF-SUB-PRD-CD.
021000     MOVE WMS-TAX-NO       TO IM-ACF-TAX-ID.
021100     MOVE SPACES           TO NB-ACF-ZIP.
021200     MOVE 1                TO X.
021300     IF  WMS-NME-ADDR-TLRS EQUAL ZERO
021400         MOVE SPACES TO NB-ACF-ZIP
021500         GO TO R3698-A.
021600     MOVE WMS-NAME-ADDRESS (1)  TO IM-ACF-NAME-ADDR (X).
021700     ADD 1                 TO X.
021800     IF WMS-NAME-ADDR-TYPE (2) = '1'
021900         MOVE WMS-NAME-ADDRESS (2) TO IM-ACF-NAME-ADDR (X).
022000     ADD 1                 TO X.
022100     IF WMS-NAME-ADDR-TYPE (2) = '2'
022200         MOVE WMS-NAME-ADDRESS (2) TO IM-ACF-NAME-ADDR (X)
022300         ADD 1             TO X.
022400     IF WMS-NAME-ADDR-TYPE (3) = '2'
022500         MOVE WMS-NAME-ADDRESS (3) TO IM-ACF-NAME-ADDR (X)
022600         ADD 1             TO X.
022700     IF WMS-NAME-ADDR-TYPE (4) = '2'
022800         IF X LESS THAN 5
022900             MOVE WMS-NAME-ADDRESS (4) TO IM-ACF-NAME-ADDR (X)
023000             ADD 1         TO X.
023100     IF WMS-NAME-ADDR-TYPE (5) = '2'
023200         IF X LESS THAN 5
023300             MOVE WMS-NAME-ADDRESS (5) TO IM-ACF-NAME-ADDR (X)
023400             ADD 1         TO X.
023500     IF WMS-NAME-ADDR-TYPE (3) = '3'
023600         MOVE WMS-CITY-STATE (3) TO IM-ACF-NAME-ADDR (5)
023700         MOVE WMS-NA-ZIP-CODE (3) TO NB-ACF-ZIP
023800         ADD 1             TO X.
023900     IF WMS-NAME-ADDR-TYPE (4) = '3'
024000         MOVE WMS-NAME-ADDRESS (4) TO IM-ACF-NAME-ADDR (5)
024100         MOVE WMS-NA-ZIP-CODE (4) TO NB-ACF-ZIP.
024200     IF WMS-NAME-ADDR-TYPE (5) = '3'
024300         MOVE WMS-NAME-ADDRESS (5) TO IM-ACF-NAME-ADDR (5)
024400         MOVE WMS-NA-ZIP-CODE (5) TO NB-ACF-ZIP.
024500     IF WMS-NAME-ADDR-TYPE (6) = '3'
024600         MOVE WMS-NAME-ADDRESS (6) TO IM-ACF-NAME-ADDR (5)
024700         MOVE WMS-NA-ZIP-CODE (6) TO NB-ACF-ZIP.
024800
024900 R3698-A.
025000     IF  NB-ACF-ZIP = '          ' OR '0000000000'
025100         IF  WMS-ZIP-CODE NOT EQUAL '     ' AND '00000'
025200             MOVE WMS-ZIP-SAVE TO NB-ACF-ZIP.
025300     MOVE NB-ACF-1-5       TO IM-ACF-ZIP-CD-5.
025400     MOVE NB-ACF-6-10      TO IM-ACF-ZIP-CD-4.
025500     IF  WMS-CMA-INDICATOR GREATER THAN '0'
025600         MOVE '1'          TO IM-ACF-SWEEP-IND
025700     ELSE
025800         MOVE ' '          TO IM-ACF-SWEEP-IND.
025900     MOVE '1'              TO IM-ACF-ABM-IND.
026000     MOVE COLL-BAL         TO IM-ACF-END-BAL.
026100     MOVE WMS-AVAIL-BAL    TO IM-ACF-AVAIL-CR.
026200     MOVE WMS-PRIN-BAL     TO IM-ACF-LOAN-BAL.
026300     MOVE 'O'              TO I-O-CONTROL-ACCESS.
026400     MOVE 'L'              TO I-O-CONTROL-OPERATOR.
026500     CALL 'IMACMTS'        USING I-O-CONTROL-AREA
026600                                 IM-ACM-ABM-TRAN.
026700 R3698-X.
026800     EXIT.
