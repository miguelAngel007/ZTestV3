*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*---------------------------------------------------------------*
000200*   MONTHLY / ANNUAL DORMANT FEE CHARGE PROCESSING              *
000300*                                                               *
000400*   THIS PROCEDURE COPYBOOK IS USED IN IM31 MODULE TO PROCESS   *
000500*   MONTHLY OR ANNUAL DORMANT FEE CHARGE ASSESSMENT FOR DORMANT *
000600*   AND ABANDONED ACCOUNTS.                                     *
000700*                                                               *
000800*   IN BCR: DORMANT FEE FREQUENCY = '2' - MONTHLY ASSESSMENT    *
000900*                                   '3' - ANNUAL ASSESSMENT     *
001000*                                                               *
001100*---------------------------------------------------------------*
001200*
001300 R7150-DORM-FEE.
001400     IF  (WMS-STATUS NOT EQUAL '06' AND '10')
001500         GO TO R7150-DORM-FEE-EXIT.
001600     IF  WMS-CURR-BAL LESS THAN +0.00
001700         GO TO R7150-DORM-FEE-EXIT.
001800     IF  WBC-YEAR-END EQUAL '1'
001900         GO TO R7150-DORM-FREQ-3.
002000     IF  WBC-MONTH-END EQUAL '1'
002100         GO TO R7150-DORM-FREQ-2.
002200     GO TO R7150-DORM-FEE-EXIT.
002300*
002400 R7150-DORM-FREQ-3.
002500     IF  WMS-MMDA-INDICATOR NOT EQUAL '1' AND '2' AND '3'         0437111
002600         IF  WBC-NB-DDA-DORM-FEE-FREQ NOT EQUAL '3'
002700             GO TO R7150-DORM-FREQ-2
002800         ELSE
002900             MOVE WBC-NB-DDA-DORM-FEE-AMT TO HOLD-AMT.
003000     IF  WMS-MMDA-INDICATOR EQUAL '1' OR '2' OR '3'               0437111
003100         IF  WBC-NB-SAV-DORM-FEE-FREQ NOT EQUAL '3'
003200             GO TO R7150-DORM-FREQ-2
003300         ELSE
003400             MOVE WBC-NB-SAV-DORM-FEE-AMT TO HOLD-AMT.
003500     PERFORM R6165-DORM-FEE-CONT THRU R6169-DORM-EXIT.
003600     GO TO R7150-CURR-BAL-CHECK.
003700*
003800 R7150-DORM-FREQ-2.
003900     IF  WMS-MMDA-INDICATOR NOT EQUAL '1' AND '2' AND '3'         0437111
004000         IF  WBC-NB-DDA-DORM-FEE-FREQ NOT EQUAL '2'
004100             GO TO R7150-DORM-FEE-EXIT
004200         ELSE
004300             MOVE WBC-NB-DDA-DORM-FEE-AMT TO HOLD-AMT.
004400     IF  WMS-MMDA-INDICATOR EQUAL '1' OR '2' OR '3'               0437111
004500         IF  WBC-NB-SAV-DORM-FEE-FREQ NOT EQUAL '2'
004600             GO TO R7150-DORM-FEE-EXIT
004700         ELSE
004800             MOVE WBC-NB-SAV-DORM-FEE-AMT TO HOLD-AMT.
004900     PERFORM R6165-DORM-FEE-CONT THRU R6169-DORM-EXIT.
005000*
005100 R7150-CURR-BAL-CHECK.
005200     IF  WMS-CURR-BAL EQUAL +0.00
005300         IF  WMS-CLS-OVRRIDE NOT EQUAL '1'
005400             GO TO R6050.
005500*
005600 R7150-DORM-FEE-EXIT.
005700     EXIT.
