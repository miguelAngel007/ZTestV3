*     * FO5238*12/05/11 JCTE PROYECTO UPGRADE SYSTEMAT
000100*----------------------------------------------------------------*
000200*    PLAN ACCOUNT PROCESSING                                     *
000300*----------------------------------------------------------------*
000400*    GENERATE 'PL' EXCEPTION FOR ALL PLAN ACCOUNTS               *
000500*----------------------------------------------------------------*
000600     MOVE '17' TO IMEX-REC-NO.
000700     MOVE 'PL' TO IMEX-CODE-1.
000800     PERFORM WRITE-EX-MST THRU WRITE-EX-EXIT.
000900*----------------------------------------------------------------*
001000*    GENERATE 'PH' EXCEPTION FOR ALL HSA PLANS                   *
001100*----------------------------------------------------------------*
001200     IF  WMS-PLN-TRLR-TYPE EQUAL '10'
001300         MOVE '17' TO IMEX-REC-NO
001400         MOVE 'PH' TO IMEX-CODE-1
001500         PERFORM WRITE-EX-MST THRU WRITE-EX-EXIT.
001600*----------------------------------------------------------------*
001700*    GENERATE 'PX' EXCEPTION FOR PARTICIPANT EXCEPTION PLANS     *
001800*----------------------------------------------------------------*
001900     IF  WMS-PLN-TRLR-PART-EXC-FLG NOT EQUAL '0'
002000         MOVE '17' TO IMEX-REC-NO
002100         MOVE 'PX' TO IMEX-CODE-1
002200         PERFORM WRITE-EX-MST THRU WRITE-EX-EXIT.
002300*----------------------------------------------------------------*
002400*    PLAN STATUS PROCESSING                                      *
002500*----------------------------------------------------------------*
002600     MOVE WBC-NEXT-CAPTURE-MO  TO WK-CPT-MM.
002700     MOVE WBC-NEXT-CAPTURE-DA  TO WK-CPT-DD.
002800     MOVE WBC-NEXT-CAPTURE-YR  TO WK-CPT-YY.
002900     IF  WBC-NEXT-CAPTURE-YR LESS THAN WBC-NEXT-CENT-YR
003000         MOVE '20'             TO WK-CPT-CC
003100     ELSE
003200         MOVE '19'             TO WK-CPT-CC.
003300     MOVE WK-CAPTURE-DATE      TO WK-LAST-DATE.
003400     IF  WMS-PLN-TRLR-RMND-YEAR NOT GREATER THAN                  1017800
003500                                  WK-CPT-YEAR                     1017800
003510         IF  WMS-PLN-TRLR-STATUS-CD LESS THAN '2'
003600             MOVE '2'             TO WMS-PLN-TRLR-STATUS-CD
003610             PERFORM K3351-CONT THRU K3351-CALC-LIMITS-EXIT.
003700     IF  WMS-PLN-TRLR-REG-MAX-DATE NOT GREATER THAN
003800                                  WK-CAPTURE-DATE
003900         MOVE '4'         TO WMS-PLN-TRLR-STATUS-CD.
004000     IF (WMS-PLN-TRLR-SNG-FAM-FLG EQUAL 'N' OR 'U')               0827778
004100     OR (WMS-PLN-TRLR-PART-EXC-FLG EQUAL '2')
004200         MOVE '1' TO WMS-PLN-TRLR-VALID-CONTR.
004300*----------------------------------------------------------------*
004400*    ACCOUNTS OVER MAXIMUM CONTRIBUTION YEAR-TO-DATE             *
004500*----------------------------------------------------------------*
004600     MOVE ZERO TO WS-WORK-CONT-DIST-KEY
004700                  WS-WORK-CTDT-AMT.
004800     MOVE '80' TO WS-WORK-PLN-CODE.
004900     MOVE SPACES TO WS-WORK-PLN-CODE2.
005000     SET WMS-PLN-TRLR-IND TO 1.
005100     SET WMS-PLN-TRLR-IND2 TO WMS-PLN-TRLR-CTDT-NO.
005200
005300 R4510-SEARCH.
005400     SEARCH WMS-PLN-TRLR-CTDT-TRLR
005500        AT END GO TO R4510-COMPARE
005600        WHEN WMS-PLN-TRLR-IND GREATER THAN WMS-PLN-TRLR-IND2
005700          OR WMS-PLN-TRLR-CTDT-KEY (WMS-PLN-TRLR-IND) GREATER THAN
005800                 WS-WORK-CONT-DIST-KEY
005900             GO TO R4510-COMPARE
006000        WHEN (WMS-PLN-TRLR-PL-CODE (WMS-PLN-TRLR-IND) EQUAL '71'
006100                                                         OR '79')
006200         AND (WMS-PLN-TRLR-CAL-YR (WMS-PLN-TRLR-IND) EQUAL '0'
006300         AND WMS-PLN-TRLR-TAX-YR (WMS-PLN-TRLR-IND)  EQUAL '0')
006400             ADD WMS-PLN-TRLR-CTDT-AMT (WMS-PLN-TRLR-IND) TO
006500                 WS-WORK-CTDT-AMT
006600             SET WMS-PLN-TRLR-IND UP BY 1
006700             GO TO R4510-SEARCH
006800        WHEN (WMS-PLN-TRLR-PL-CODE (WMS-PLN-TRLR-IND) EQUAL '22')
006900         AND (WMS-PLN-TRLR-CAL-YR (WMS-PLN-TRLR-IND) EQUAL '0'
007000         AND WMS-PLN-TRLR-TAX-YR (WMS-PLN-TRLR-IND)  EQUAL '0')
007100             SUBTRACT WMS-PLN-TRLR-CTDT-AMT (WMS-PLN-TRLR-IND)
007200                 FROM WS-WORK-CTDT-AMT
007300             SET WMS-PLN-TRLR-IND UP BY 1
007400             GO TO R4510-SEARCH.
007500
007600 R4510-COMPARE.
007700     IF  WS-WORK-CTDT-AMT GREATER THAN WMS-PLN-TRLR-ACT-CNT-LIM
007800         MOVE '17' TO IMEX-REC-NO
007900         MOVE 'PO' TO IMEX-CODE-1
008000         MOVE '  ' TO IMEX-CODE-2
008100         PERFORM WRITE-EX-MST THRU WRITE-EX-EXIT.
008200*----------------------------------------------------------------*
008300*    ACCOUNTS OVER MAXIMUM CONTRIBUTION PRIOR YEAR               *
008400*----------------------------------------------------------------*
008500     MOVE ZERO TO WS-WORK-CTDT-AMT.
008600     SET WMS-PLN-TRLR-IND TO 1.
008700     SET WMS-PLN-TRLR-IND2 TO WMS-PLN-TRLR-CTDT-NO.
008800
008900 R4520-REENTER.
009000     SEARCH WMS-PLN-TRLR-CTDT-TRLR
009100        AT END GO TO R4520-COMPARE
009200        WHEN WMS-PLN-TRLR-IND GREATER THAN WMS-PLN-TRLR-IND2
009300             GO TO R4520-COMPARE
009400        WHEN (WMS-PLN-TRLR-PL-CODE (WMS-PLN-TRLR-IND) EQUAL '71'
009500                                                         OR '79')
009600         AND ((WMS-PLN-TRLR-CAL-YR (WMS-PLN-TRLR-IND) EQUAL '0'
009700         AND WMS-PLN-TRLR-TAX-YR (WMS-PLN-TRLR-IND)   EQUAL '1')
009800          OR (WMS-PLN-TRLR-CAL-YR (WMS-PLN-TRLR-IND)  EQUAL '1'
009900         AND WMS-PLN-TRLR-TAX-YR (WMS-PLN-TRLR-IND)   EQUAL '0'))
010000             ADD WMS-PLN-TRLR-CTDT-AMT (WMS-PLN-TRLR-IND) TO
010100                 WS-WORK-CTDT-AMT
010200             SET WMS-PLN-TRLR-IND UP BY 1
010300             GO TO R4520-REENTER
010400        WHEN (WMS-PLN-TRLR-PL-CODE (WMS-PLN-TRLR-IND) EQUAL '22')
010500         AND (WMS-PLN-TRLR-TAX-CODE (WMS-PLN-TRLR-IND) EQUAL '0')
010600         AND (((WMS-PLN-TRLR-CAL-YR (WMS-PLN-TRLR-IND) EQUAL '0')
010700         AND (WMS-PLN-TRLR-TAX-YR (WMS-PLN-TRLR-IND)   EQUAL '1'))
010800          OR ((WMS-PLN-TRLR-CAL-YR (WMS-PLN-TRLR-IND)  EQUAL '1')
010900         AND (WMS-PLN-TRLR-TAX-YR (WMS-PLN-TRLR-IND)  EQUAL '0')))
011000             SUBTRACT WMS-PLN-TRLR-CTDT-AMT (WMS-PLN-TRLR-IND)
011100                 FROM WS-WORK-CTDT-AMT
011200             SET WMS-PLN-TRLR-IND UP BY 1
011300             GO TO R4520-REENTER.
011400
011500 R4520-COMPARE.
011600     IF  WS-WORK-CTDT-AMT GREATER THAN WMS-PLN-TRLR-PY-ACT-CNT-LIM
011700         MOVE '17' TO IMEX-REC-NO
011800         MOVE 'PY' TO IMEX-CODE-1
011900         MOVE '  ' TO IMEX-CODE-2
012000         PERFORM WRITE-EX-MST THRU WRITE-EX-EXIT.
012100*----------------------------------------------------------------*
012200*    MONTH-END PROCESSING FOR PLAN ACCOUNTS                      *
012300*----------------------------------------------------------------*
012400     IF  WBC-MONTH-END NOT EQUAL '1'
012500         GO TO R4550.
012600     IF (WMS-PLN-TRLR-SNG-FAM-FLG EQUAL 'N' OR 'U')               0827778
012700         MOVE WMS-PLN-TRLR-ACC-CNT-LIM TO WMS-PLN-TRLR-ACT-CNT-LIM
012800         GO TO R4550.
012900     IF  WMS-PLN-TRLR-MAX-CNT-PNT EQUAL ZERO
013000         MOVE WMS-PLN-TRLR-MAX-CNT-LIM
013100                             TO HOLD-AMT13
013200     ELSE
013300     SET WBC-PL-LMT-IND      TO WMS-PLN-TRLR-MAX-CNT-PNT
013400         IF  WMS-PLN-TRLR-STATUS-CD GREATER THAN '1'
013500             IF  WBC-NEXT-YEAR-END EQUAL '1'
013600                 IF  WBC-PL-PY-CU-MAX-CONT (WBC-PL-LMT-IND) > +0
013700                     MOVE WBC-PL-PY-CU-MAX-CONT (WBC-PL-LMT-IND)
013800                                 TO HOLD-AMT13
013900                 ELSE
014000                     MOVE WBC-PL-PY-MAX-CONT (WBC-PL-LMT-IND)
014100                                 TO HOLD-AMT13
014200             ELSE
014300             IF  WBC-PL-CY-CU-MAX-CONT (WBC-PL-LMT-IND) > +0
014400                 MOVE WBC-PL-CY-CU-MAX-CONT (WBC-PL-LMT-IND)
014500                                 TO HOLD-AMT13
014600             ELSE
014700                 MOVE WBC-PL-CY-MAX-CONT (WBC-PL-LMT-IND)
014800                                 TO HOLD-AMT13
014900         ELSE
015000         IF  WBC-NEXT-YEAR-END EQUAL '1'
015100             MOVE WBC-PL-PY-MAX-CONT (WBC-PL-LMT-IND)
015200                                 TO HOLD-AMT13
015300         ELSE
015400             MOVE WBC-PL-CY-MAX-CONT (WBC-PL-LMT-IND)
015500                                 TO HOLD-AMT13.
015600     IF  HOLD-AMT13 GREATER THAN +0
015700         COMPUTE HOLD-AMT7 ROUNDED = HOLD-AMT13 / +12
015800     ELSE
015900         MOVE +0 TO HOLD-AMT7.
016000*
016100* WK-CAPTURE-DATE IS THE WBC-NEXT-CAPTURE-DATE AT THIS POINT
016200*
016300     IF  WMS-PLN-TRLR-ELIG-YEAR GREATER THAN WK-CPT-YEAR
016400         GO TO R4550
016500     ELSE
016600         IF  WMS-PLN-TRLR-ELIG-YEAR EQUAL WK-CPT-YEAR
016700             IF  WMS-PLN-TRLR-ELIG-MO GREATER THAN WK-CPT-MM
016800                 GO TO R4550
016900             ELSE
017000                 IF  WMS-PLN-TRLR-ELIG-MO EQUAL WK-CPT-MM
017100                     IF  WMS-PLN-TRLR-ELIG-DA EQUAL '01'
017200                         GO TO R4540-CALC-LIMITS
017300                     ELSE
017400                         GO TO R4550.
017500*
017600 R4540-CALC-LIMITS.
017700     IF  WBC-NEXT-CAPTURE-MO  EQUAL '01'
017900         MOVE HOLD-AMT7 TO WMS-PLN-TRLR-ACC-CNT-LIM
018000     ELSE
018100         ADD HOLD-AMT7  TO WMS-PLN-TRLR-ACC-CNT-LIM.
018200
018300 R4550.
018400*----------------------------------------------------------------*
018500*    YEAR-END PROCESSING FOR PLAN ACCOUNTS                       *
018600*----------------------------------------------------------------*
018700     IF  WBC-YEAR-END NOT EQUAL '1'
018800         GO TO R4600.
018900     MOVE WMS-PLN-TRLR-MAX-CNT-LIM TO WMS-PLN-TRLR-PY-MAX-CNT.
019000     MOVE WMS-PLN-TRLR-ACT-CNT-LIM TO WMS-PLN-TRLR-PY-ACT-CNT-LIM.
019100     IF (WMS-PLN-TRLR-SNG-FAM-FLG EQUAL 'N' OR 'U')               0827778
019200         MOVE +0        TO WMS-PLN-TRLR-ACC-CNT-LIM
019300                           WMS-PLN-TRLR-ACT-CNT-LIM
019400         GO TO R4600.
019500     MOVE WMS-PLN-TRLR-ELIG-MO TO WK-PLN-TRLR-ELIG-MO9.
019600     IF  WMS-PLN-TRLR-ELIG-YEAR GREATER THAN WK-CPT-YEAR
019700         NEXT SENTENCE
019800     ELSE
019900         IF  WMS-PLN-TRLR-ELIG-YEAR EQUAL WK-CPT-YEAR
020000             IF  WMS-PLN-TRLR-ELIG-DA EQUAL '01'
020100                 COMPUTE HOLD-9 = 13 - WK-PLN-TRLR-ELIG-MO9
020200             ELSE
020300                 COMPUTE HOLD-9 = 12 - WK-PLN-TRLR-ELIG-MO9
020400             END-IF
020500             COMPUTE HOLD-ACT-AMT ROUNDED = HOLD-AMT7 * HOLD-9
020600             IF  HOLD-9 NOT EQUAL 0                               1017800
020700                 MOVE HOLD-AMT13      TO WMS-PLN-TRLR-ACT-CNT-LIM
020800             ELSE
020900                 MOVE +0              TO WMS-PLN-TRLR-ACT-CNT-LIM 1017800
021000             END-IF
021100         ELSE
021200             MOVE HOLD-AMT13       TO WMS-PLN-TRLR-ACT-CNT-LIM.
021300     MOVE WMS-PLN-TRLR-MAX-CNT-PNT TO WMS-PLN-TRLR-PY-MAX-CNT-PNT.
021400     MOVE WMS-PLN-TRLR-STATUS-CD   TO WMS-PLN-TRLR-PY-STATUS-CD.
021500     PERFORM CALC-FAIR-MARKET-VALUE THRU CFMV-EXIT.
