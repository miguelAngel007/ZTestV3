000100*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
000200*   THIS IS PROCEDURE DIVISION CODE TO MAINTAIN DEBIT AND CREDIT *
000300*   TOTALS.                                                      *
000400*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
000500*
000600*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
000700*  THE INITIAL START BROWSE WILL BE PERFORMED AT COMPANY CHANGE  *
000800*  WHEN POSITIONS 3 AND 4 OF THE CCR CO NBR EQUAL ZERO           *
000900*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
001000 IB-INITIAL-START-BROWSE.
001100*  DO NOT READ DEBIT CREDIT MASTER IF EMPTY OR END OF FILE       *
001200     MOVE  'N'                       TO WS-DBCR-MATCH-MASTER.
001300     MOVE  'Y'                       TO WS-DBCR-THIS-CO.
001400     MOVE  'Y'                       TO WS-PROCESSED-DBCR-FILE.
001500     MOVE  HIGH-VALUES               TO WS-DBCR-KEY.
001600     MOVE  'Y'                       TO WS-FIRST-RECORD.
001700     IF  (WS-FM-DB-CR-FILE-EMPTY EQUAL 'Y')
001800     OR  (WS-DBCR-EOF EQUAL 'Y')
001900         MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
002000         MOVE  'N'                   TO WS-DBCR-THIS-CO
002100         GO TO IB-INITIAL-START-BROWSE-EXIT.
002200     MOVE  CC-CUST-NO                TO FM-DBCR-CO-NUMBER.
002300     MOVE  ZEROES                    TO FM-DBCR-RC-NUMBER.
002400     MOVE  ZEROES                    TO FM-DBCR-ACCT-NUMBER.
002500     MOVE 'T'                        TO I-O-CONTROL-OPERATOR.
002600     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
002700                          FM-DBCR-ACCT-MASTER.
002800     IF  NOT I-O-88-NORMAL-RET
002900         IF   FM-DBCR-CO-NUMBER EQUAL CC-CUST-NO
003000              MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
003100              MOVE 'N'                    TO WS-DBCR-THIS-CO
003200              MOVE 'Y'                    TO WS-DBCR-EOF
003300              GO TO IB-INITIAL-START-BROWSE-EXIT
003400         ELSE
003500              MOVE 'N'                    TO WS-DBCR-THIS-CO
003600              GO TO IB-INITIAL-START-BROWSE-EXIT.
003700*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
003800*   THE FIRST READ AT COMPANY CHANGE SHOULD POSITION THE DEBIT   *
003900*   CREDIT MASTER AT THE SAME COMPANY AS THE ACCOUNT MASTER.     *
004000*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
004100 IB-INITIAL-FIRST-READ.
004200     MOVE 'R'                        TO I-O-CONTROL-OPERATOR.
004300
004400     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
004500                          FM-DBCR-ACCT-MASTER.
004600     IF  I-O-88-END-OF-FILE
004700         MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
004800         MOVE 'N'                    TO WS-DBCR-THIS-CO
004900         MOVE 'Y'                    TO WS-DBCR-EOF
005000         MOVE 'N'                    TO WS-DBCR-MATCH-MASTER
005100         GO TO IB-INITIAL-FIRST-READ-EXIT.
005200     MOVE FM-DBCR-KEY                TO WS-DBCR-KEY.
005210     MOVE FM-DBCR-ACCT-MASTER        TO WS-HOLD-DBCR-RECORD.
005300     IF  FM-DBCR-CO-NUMBER LESS THAN CC-CUST-NO
005400         GO TO IB-INITIAL-FIRST-READ.
005500     IF  FM-DBCR-CO-NUMBER EQUAL  CC-CUST-NO
005600         MOVE 'Y'                    TO WS-DBCR-THIS-CO
005700     ELSE
005800         MOVE 'N'                    TO WS-DBCR-THIS-CO.
005900 IB-INITIAL-FIRST-READ-EXIT.
006000     EXIT.
006100 IB-INITIAL-START-BROWSE-EXIT.
006200     EXIT.
006300
006400*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
006500*  THE DEBIT CREDIT MASTER IS READ EACH TIME AN FMS ACCOUNT      *
006600*  MASTER IS ACCESSED.  IF THE DEBIT CREDIT MASTER IS AT END OF  *
006700*  FILE, OR THERE ARE NO ACCOUNTS FOR THIS COMPANY OR THE DEBIT  *
006800*  CREDIT FILE IS EMPTY, THE READ IS SKIPPED.  IF  THE FIRST     *
006900*  RECORD FOR THE CURRENT CCR COMPANY HAS BEEN ACCESSED, THE     *
007000*  MASTER READ IS SKIPPED.  IF THE DEBIT CREDIT MASTER RECORD    *
007100*  KEY IS LESS THAN THE FMS ACCOUNT MASTER KEY, THE RECORD IS    *
007200*  DELETED.  IF THE DEBIT CREDIT MASTER RECORD KEY IS GREATER    *
007300*  THAN THE FMS ACCOUNT MASTER KEY, A RECORD FOR THE FMS RECORD  *
007400*  IS ADDED TO THE DEBIT CREDIT MASTER.                          *
007500*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
007600 IB-READ-MASTER.
007700     IF  (WS-DBCR-EOF EQUAL 'Y')
007800     OR  (WS-FM-DB-CR-FILE-EMPTY EQUAL 'Y')
007900     OR  (WS-DBCR-THIS-CO EQUAL 'N')
008000         MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
008100         MOVE WK-KEY                 TO FM-DBCR-KEY
008200         MOVE HIGH-VALUES            TO WS-DBCR-KEY
008300         MOVE 'N'                    TO WS-DBCR-MATCH-MASTER
008400         MOVE 'N'                    TO WS-FIRST-RECORD
008500         GO TO IB-READ-MASTER-EXIT.
008600     IF  WS-FIRST-RECORD EQUAL 'Y'
008700         MOVE 'N' TO WS-FIRST-RECORD
008800         IF  WS-DBCR-KEY LESS THAN WK-KEY
008900             PERFORM  IB-DELETE-ACCT THRU IB-DELETE-ACCT-EXIT
009000             GO TO IB-READ-MASTER-CONT.
009100     IF  WS-DBCR-KEY GREATER THAN WK-KEY
009200         MOVE 'N'                    TO WS-DBCR-MATCH-MASTER
009300         MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
009400         MOVE WK-KEY                 TO FM-DBCR-KEY
009500         GO TO IB-READ-MASTER-EXIT.
009600     IF  WS-DBCR-KEY EQUAL WK-KEY
009700         MOVE 'Y'                    TO WS-DBCR-MATCH-MASTER
009710         MOVE WS-HOLD-DBCR-RECORD    TO FM-DBCR-ACCT-MASTER
009800         GO TO IB-READ-MASTER-EXIT.
009900 IB-READ-MASTER-CONT.
010000     MOVE 'R'                        TO I-O-CONTROL-OPERATOR.
010100
010200     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
010300                          FM-DBCR-ACCT-MASTER.
010400     IF  I-O-88-END-OF-FILE
010500         MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
010600         MOVE 'Y'                    TO WS-DBCR-EOF
010700         MOVE 'N'                    TO WS-DBCR-MATCH-MASTER
010800         MOVE WK-KEY                 TO FM-DBCR-KEY
010900         MOVE HIGH-VALUES            TO WS-DBCR-KEY
011000         GO TO IB-READ-MASTER-EXIT.
011100     MOVE FM-DBCR-KEY                TO WS-DBCR-KEY.
011110     MOVE FM-DBCR-ACCT-MASTER        TO WS-HOLD-DBCR-RECORD.
011200     IF  FM-DBCR-KEY LESS THAN WK-KEY
011300         PERFORM IB-DELETE-ACCT THRU IB-DELETE-ACCT-EXIT
011400         GO TO IB-READ-MASTER-CONT.
011500     IF  FM-DBCR-KEY EQUAL WK-KEY
011600         MOVE 'Y'                    TO WS-DBCR-MATCH-MASTER
011700     ELSE
011800         MOVE 'N'                    TO WS-DBCR-MATCH-MASTER
011900         MOVE FM-DFDC-ACCT-MASTER    TO FM-DBCR-ACCT-MASTER
012000         MOVE WK-KEY                 TO FM-DBCR-KEY.
012100     IF  FM-DBCR-CO-NUMBER NOT EQUAL WK-CUST-NO
012200         MOVE 'N' TO WS-DBCR-THIS-CO.
012300 IB-READ-MASTER-EXIT.
012400     EXIT.
012500
012600 IB-TOTALING.
012700*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
012800*    POSTABLE ACCOUNT BALANCES  ARE TABLED                     *
012900*    SO THAT TOTAL ACCOUNTS MAY BE CALCULATED                  *
013000*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
013100     IF  WK-BRACKET NOT EQUAL ZERO
013200         GO TO IB-TOTALINGB.
013300     MOVE FM-DBCR-DB-THIS-MO         TO TC-DBCR-DB-THIS-MO.
013400     MOVE FM-DBCR-CR-THIS-MO         TO TC-DBCR-CR-THIS-MO.
013500     MOVE FM-DBCR-PRES-YR-DB-EOM-TBL
013600                             TO TC-DBCR-PRESENT-YR-DB-EOM-TBL.
013700     MOVE FM-DBCR-PRES-YR-CR-EOM-TBL
013800                             TO TC-DBCR-PRESENT-YR-CR-EOM-TBL.
013900     MOVE FM-DBCR-PRIOR-YR-DB-EOM-TBL
014000                             TO TC-DBCR-PRIOR-YR-DB-EOM-TBL.
014100     MOVE FM-DBCR-PRIOR-YR-CR-EOM-TBL
014200                             TO TC-DBCR-PRIOR-YR-CR-EOM-TBL.
014300 IB-TOTALINGB.
014400*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
014500*    THE TOTALLING ROUTINE IS CALLED SO THAT TOTAL ACCOUNT       *
014600*    DEBIT/CREDIT BALANCES WILL BE CALCULATED FROM THE           *
014700*    POSTABLE ACCOUNTS.                                          *
014800*    U IS EQUAL TO NUMBER OF BALANCES BEING TOTALED              *
014900*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
015000     IF  (WK-BRACKET EQUAL ZERO)
015100         AND (TRANSFER-CTRS3  EQUAL   ZEROED-COUNTERS3)
015200             GO TO   IB-TOTALING-EXIT.
015300     MOVE NUM-TRANSFER-CTRS3         TO U.
015410*    15 MAY 1997 : NO CONSIDERA DEBIT/CREDIT FLAG EN ACUMULACION
015420*                  DE LAS CUENTAS DE TOTALES
015400     CALL  'FMPLTIB'  USING U  V  PRINT-LEVEL-TOTALS3
015500         TRANSFER-CTRS3 WK-MISC-FLAG.
015600 IB-TOTALINGC.
015700*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
015800*    WHEN A TOTAL ACCOUNT IS ENCOUNTERED, THE TOTAL ACCOUNT      *
015900*    DEBIT/CREDIT BALANCES ARE  CALCULATED FROM THE              *
016000*    POSTABLE ACCOUNTS.                                          *
016100*    U IS EQUAL TO NUMBER OF BALANCES BEING TOTALED              *
016200*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
016300     IF  WK-BRACKET EQUAL ZERO
016400         GO TO IB-TOTALING-EXIT.
016500     MOVE TC-DBCR-DB-THIS-MO         TO FM-DBCR-DB-THIS-MO.
016600     MOVE TC-DBCR-CR-THIS-MO         TO FM-DBCR-CR-THIS-MO.
016700     MOVE TC-DBCR-PRESENT-YR-DB-EOM-TBL
016800                                TO FM-DBCR-PRES-YR-DB-EOM-TBL.
016900     MOVE TC-DBCR-PRESENT-YR-CR-EOM-TBL
017000                                TO FM-DBCR-PRES-YR-CR-EOM-TBL.
017100     MOVE TC-DBCR-PRIOR-YR-DB-EOM-TBL
017200                                TO FM-DBCR-PRIOR-YR-DB-EOM-TBL.
017300     MOVE TC-DBCR-PRIOR-YR-CR-EOM-TBL
017400                                TO FM-DBCR-PRIOR-YR-CR-EOM-TBL.
017500 IB-TOTALING-EXIT.
017600     EXIT.
017700*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
017800*  THE MONETARY BALANCES, ACCT TITLE, ETC ARE MOVED TO THE       *
017900*  DEBIT CREDIT MASTER FROM THE FMS ACCOUNT MASTER  AFTER ALL    *
018000*  TRANSACTIONS FOR THE ACCOUNT HAVE BEEN APPLIED.               *
018100*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
018200 IB-MOVE-ACCT-MASTER.
018300     MOVE WK-REG-ACCT-NBR            TO FM-DBCR-REG-ACCT-NUMBER.
018400     MOVE WK-ACCT-DESC               TO FM-DBCR-ACCT-DESC.
018500     MOVE WK-ACCT-TYPE               TO FM-DBCR-ACCT-TYPE.
018600     MOVE WK-PRINT-LEVEL             TO FM-DBCR-PRINT-LEVEL.
018700     MOVE WK-BRACKET                 TO FM-DBCR-BRACKET.
018800     MOVE WK-DB-CR                   TO FM-DBCR-DB-CR.
018900     MOVE WK-ALLOC-FLAG              TO FM-DBCR-ALLOC-FLAG.
019000     MOVE WK-HEADING-BRACKET         TO FM-DBCR-HEADING-BRACKET.
019100     MOVE WK-MEMO-PROFIT             TO FM-DBCR-MEMO-PROFIT.
019200     MOVE WK-USER-TYPE (1)           TO FM-DBCR-USER-TYPE1.
019300     IF  WK-MEMO-PROFIT EQUAL '4'
019400         MOVE 'CC'                   TO FM-DBCR-USER-TYPE1.
019500     MOVE WK-CONTROL-SORT            TO FM-DBCR-CONTROL-SORT.
019600     MOVE CC-FS-MO                   TO FM-DBCR-CC-FS-MO.
019700     MOVE CC-FS-DA                   TO FM-DBCR-CC-FS-DA.
019800     MOVE CC-FS-YR                   TO FM-DBCR-CC-FS-YR.
019900     MOVE CC-NO-MO-CLOSED            TO FM-DBCR-CC-NO-MO-CLOSED.
020000     MOVE WK-PRESENT-YR-EOM (1)    TO FM-DBCR-PRESENT-YR-EOM (1).
020100     MOVE WK-PRESENT-YR-EOM (2)    TO FM-DBCR-PRESENT-YR-EOM (2).
020200     MOVE WK-PRESENT-YR-EOM (3)    TO FM-DBCR-PRESENT-YR-EOM (3).
020300     MOVE WK-PRESENT-YR-EOM (4)    TO FM-DBCR-PRESENT-YR-EOM (4).
020400     MOVE WK-PRESENT-YR-EOM (5)    TO FM-DBCR-PRESENT-YR-EOM (5).
020500     MOVE WK-PRESENT-YR-EOM (6)    TO FM-DBCR-PRESENT-YR-EOM (6).
020600     MOVE WK-PRESENT-YR-EOM (7)    TO FM-DBCR-PRESENT-YR-EOM (7).
020700     MOVE WK-PRESENT-YR-EOM (8)    TO FM-DBCR-PRESENT-YR-EOM (8).
020800     MOVE WK-PRESENT-YR-EOM (9)    TO FM-DBCR-PRESENT-YR-EOM (9).
020900     MOVE WK-PRESENT-YR-EOM (10)   TO FM-DBCR-PRESENT-YR-EOM (10).
021000     MOVE WK-PRESENT-YR-EOM (11)   TO FM-DBCR-PRESENT-YR-EOM (11).
021100     MOVE WK-PRESENT-YR-EOM (12)   TO FM-DBCR-PRESENT-YR-EOM (12).
021200     MOVE WK-PRIOR-YR-EOM-TABLE    TO FM-DBCR-PRIOR-YR-EOM-TBL.
021300     MOVE WK-PRESENT-YR-EOM (13)   TO FM-DBCR-PRIOR-YR-BEG-BAL.
021400     MOVE WK-PRESENT-YR-EOM (14)   TO FM-DBCR-PRESENT-YR-BEG-BAL.
021500     MOVE WK-CUR-BAL               TO FM-DBCR-CUR-BAL.
021600     IF  CC-PROC-YR-END EQUAL 'Y'
021700         MOVE CC-FE-MO             TO WS-DBCR-MOX
021800         IF  WK-ACCT-TYPE EQUAL '1' OR '2' OR '3' OR '7'
021900             MOVE WK-PRESENT-YR-EOM (WS-DBCR-MO9)
022000                                   TO FM-DBCR-PRESENT-YR-BEG-BAL
022100         ELSE
022200             MOVE ZERO             TO FM-DBCR-PRESENT-YR-BEG-BAL.
022300*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
022400*    IF THE FMS ACCOUNT MASTER KEY IS EQUAL TO THE DEBIT CREDIT  *
022500*    MASTER KEY, THE ACCOUNT IS REWRITTEN.  IF  THE FMS ACCOUNT  *
022600*    IS TO BE DELETED, THE DEBIT CREDIT ACCOUNT WILL BE DELETED. *
022700*    IF THE KEYS ARE NOT EQUAL, THE FMS ACCOUNT WILL BE ADDED TO *
022800*    THE DEBIT CREDIT MASTER.                                    *
022900*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
023000 IB-WRITE-ACCT.
023100     MOVE WK-KEY                   TO WS-LAST-PROC-KEY.
023200     IF  WK-STATUS EQUAL 'D'
023300         IF  WS-DBCR-MATCH-MASTER EQUAL 'N'
023400             GO TO IB-WRITE-ACCT-EXIT
023500         ELSE
023600             PERFORM IB-DELETE-ACCT THRU IB-DELETE-ACCT-EXIT
023700             MOVE 'N' TO WS-DBCR-MATCH-MASTER
023800             GO TO IB-WRITE-ACCT-EXIT.
023900     IF  WS-FM-DB-CR-FILE-EMPTY EQUAL 'Y'
024000         MOVE 'L'                    TO I-O-CONTROL-OPERATOR
024100         GO TO IB-WRITE-ACCT-CONT.
024200     IF  WS-DBCR-MATCH-MASTER EQUAL 'Y'
024300         MOVE 'W'                    TO I-O-CONTROL-OPERATOR
024400         GO TO IB-WRITE-ACCT-CONT.
024500     MOVE 'A'                        TO I-O-CONTROL-OPERATOR.
024600
024700 IB-WRITE-ACCT-CONT.
024800     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
024900                          FM-DBCR-ACCT-MASTER.
025000     MOVE WK-KEY              TO WS-DBCR-KEY.
025100     IF  WS-DBCR-MATCH-MASTER EQUAL 'Y'
025200         MOVE 'N'             TO WS-DBCR-MATCH-MASTER
025300         GO TO IB-WRITE-ACCT-EXIT.
025400     IF  WS-FM-DB-CR-FILE-EMPTY EQUAL 'Y'
025500         GO TO IB-WRITE-ACCT-EXIT.
025600     IF  WS-DBCR-EOF EQUAL  'Y'
025700         GO TO IB-WRITE-ACCT-EXIT.
025800     IF  WS-DBCR-THIS-CO EQUAL 'N'
025900         GO TO IB-WRITE-ACCT-EXIT.
026000*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
026100*    IF THE ACCOUNT WAS ADDED TO THE DEBIT CREDIT MASTER, THE    *
026200*    DEBIT CREDIT MASTER WILL BE REPOSITIONED FOR A SEQUENTIAL   *
026300*    READ UNLESS ALL ACCOUNTS FOR THIS COMPANY ARE BEING ADDED OR*
026400*    THE MASTER WAS EMPTY AND IS BEING LOADED WITH AN 'L'        *
026500*    OPERATOR.                                                   *
026600*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
026700     MOVE WK-KEY                 TO FM-DBCR-KEY.
026800     MOVE 'S'                    TO I-O-CONTROL-OPERATOR.
026900     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
027000                          FM-DBCR-ACCT-MASTER.
027100     MOVE 'R'                    TO I-O-CONTROL-OPERATOR
027200     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
027300                              FM-DBCR-ACCT-MASTER.
027400 IB-WRITE-ACCT-EXIT.
027500     EXIT.
027600*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
027700* THE FMS ACCOUNT WITH THIS KEY IS BEING DELETED OR HAS BEEN     *
027800* DELETED WITH PROGRAM FM99.  DELETE THIS ACCOUNT FROM THE       *
027900* DEBIT CREDIT MASTER, ALSO.                                     *
028000*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
028100 IB-DELETE-ACCT.
028200     MOVE 'D'                        TO I-O-CONTROL-OPERATOR.
028300
028400     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
028500                          FM-DBCR-ACCT-MASTER.
028600 IB-DELETE-ACCT-EXIT.
028700     EXIT.
028800
028900*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
029000* NORMAL DEBITS AND CREDITS ADD INTO THIS MO UNLESS IT IS YEAR   *
029100* END.  YEAR END TRANSACTIONS ADD INTO THE YEAR END MONTH.       *
029200*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
029300 IB-DBCR.
029400     MOVE CC-PT-MO TO WS-DBCR-MOX.
029500     IF  TR-TRAN-CODE EQUAL '52' OR '62' OR '72'
029600         GO TO IB-CR.
029700     IF  CC-PROC-YR-END EQUAL 'Y'
029800         ADD TR-AMOUNT TO FM-DBCR-PRESENT-YR-DB-EOM (WS-DBCR-MO9)
029900         GO TO IB-DBCR-EXIT
030000     ELSE
030100         ADD TR-AMOUNT TO FM-DBCR-DB-THIS-MO
030200         GO TO IB-DBCR-EXIT.
030300 IB-DB-EXIT.
030400     EXIT.
030500 IB-CR.
030600     IF  CC-PROC-YR-END EQUAL 'Y'
030700         ADD TR-AMOUNT TO FM-DBCR-PRESENT-YR-CR-EOM (WS-DBCR-MO9)
030800         GO TO IB-DBCR-EXIT
030900     ELSE
031000         ADD TR-AMOUNT TO FM-DBCR-CR-THIS-MO.
031100 IB-CR-EXIT.
031200     EXIT.
031300 IB-DBCR-EXIT.
031400     EXIT.
031500*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
031600* EACH HISTORICAL TRANSACTION ADDS INTO THE MONTH OF ITS         *
031700* EFFECTIVE DATE.  PRIOR YEAR TRANSACTIONS ADD INTO THE          *
031800* APPROPRIATE PRIOR YEAR END MONTH.                              *
031900*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
032000 IB-DBCR-HA.
032100     MOVE TR-TR-MO TO WS-DBCR-MOX.
032200     IF  TR-PRIOR-YR-HIST EQUAL '1' OR '2'
032300         GO TO IB-DBCR-HA-PRIOR.
032400     IF  TR-TR-MO EQUAL CC-PT-MO
032500         IF  TR-TRAN-CODE EQUAL '53' OR '73'
032600             ADD TR-AMOUNT TO FM-DBCR-DB-THIS-MO
032700             GO TO IB-DBCR-HA-EXIT
032800         ELSE
032900             ADD TR-AMOUNT TO FM-DBCR-CR-THIS-MO
033000             GO TO IB-DBCR-HA-EXIT.
033100     IF  TR-TRAN-CODE EQUAL '53' OR '73'
033200         ADD TR-AMOUNT TO FM-DBCR-PRESENT-YR-DB-EOM (WS-DBCR-MO9)
033300         GO TO IB-DBCR-HA-EXIT
033400     ELSE
033500         ADD TR-AMOUNT TO FM-DBCR-PRESENT-YR-CR-EOM (WS-DBCR-MO9)
033600         GO TO IB-DBCR-HA-EXIT.
033700 IB-DBCR-HA-PRIOR.
033710     IF  TR-PRIOR-YR-HIST EQUAL '2'
033720         GO TO IB-DBCR-HA-EXIT.
033800     IF  TR-TRAN-CODE EQUAL '53' OR '73'
033900         ADD TR-AMOUNT TO FM-DBCR-PRIOR-YR-DB-EOM (WS-DBCR-MO9)
034000         GO TO IB-DBCR-HA-EXIT
034100     ELSE
034200         ADD TR-AMOUNT TO FM-DBCR-PRIOR-YR-CR-EOM (WS-DBCR-MO9).
034300 IB-DBCR-HA-PRIOR-EXIT.
034400     EXIT.
034500 IB-DBCR-HA-EXIT.
034600     EXIT.
034700*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
034800* THE FOLLOWING PARAGRAPHS WILL BE PROCESSED (IF THE DEBIT     *
034900* CREDIT MASTER HAS PROCESSED) WHEN THE FMS ACCOUNT MASTER     *
035000* REACHES END OF FILE TO DELETE ANY  RECORDS THAT EXIST ON     *
035100* THE DEBIT CREDIT MASTER BUT NOT ON THE ACCOUNT MASTER.       *
035200*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
035300 IB-DBCR-FMS-MASTER-EOF.
035400     IF  WS-FM-DB-CR-FILE-EMPTY EQUAL 'Y'
035500         GO TO IB-DBCR-FMS-MASTER-EOF-EXIT.
035600     IF  WS-DBCR-EOF EQUAL 'Y'
035700         GO TO IB-DBCR-FMS-MASTER-EOF-EXIT.
035800     MOVE WS-LAST-PROC-KEY       TO FM-DBCR-KEY.
035900     MOVE 'T'                    TO I-O-CONTROL-OPERATOR
036000     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
036100                              FM-DBCR-ACCT-MASTER.
036200     IF  NOT I-O-88-NORMAL-RET
036300         GO TO IB-DBCR-FMS-MASTER-EOF-EXIT.
036400     MOVE 'R'                    TO I-O-CONTROL-OPERATOR
036500     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
036600                              FM-DBCR-ACCT-MASTER.
036700     IF  I-O-88-END-OF-FILE
036800         GO TO IB-DBCR-FMS-MASTER-EOF-EXIT.
036900     IF  FM-DBCR-KEY GREATER THAN WS-LAST-PROC-KEY
037000         PERFORM IB-DELETE-ACCT THRU IB-DELETE-ACCT-EXIT.
037100 IB-DBCR-FMS-MASTER-EOF-READ.
037200     MOVE 'R'                    TO I-O-CONTROL-OPERATOR
037300     CALL 'FMDBCRV' USING I-O-CONTROL-AREA
037400                              FM-DBCR-ACCT-MASTER.
037500     IF  I-O-88-END-OF-FILE
037600         GO TO IB-DBCR-FMS-MASTER-EOF-EXIT.
037700     PERFORM IB-DELETE-ACCT THRU IB-DELETE-ACCT-EXIT.
037800     GO TO IB-DBCR-FMS-MASTER-EOF-READ.
037900 IB-DBCR-FMS-MASTER-EOF-EXIT.
038000     EXIT.