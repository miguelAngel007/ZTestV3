         MACRO                                                          00010000
&TAG     EPROC  &NAME                                                   00020000
.* -----------------------------------------------------------------    00030000
.* 5799-BNT (C) COPYRIGHT IBM AMERICAS/FAR EAST 1983                    00040000
.* 5799-BNT (C) COPYRIGHT IBM WORLD TRADE CORP. 1983                    00050000
.* 5798-CLF COPYRIGHT IBM CORP. 1976                                    00060000
.* LICENSED MATERIAL - PROGRAM PROPERTY OF IBM                          00070000
.* REFER TO COPYRIGHT INSTRUCTIONS FROM NO. G120-2083                   00080000
.* -----------------------------------------------------------------    00090000
.*                                                                      00100000
 GBLA &$PMAFO      * AND FOLLOWED BY OR COUNTER                         00110000
 GBLA &$PMCNO      * CASE NUMBER                                        00120000
 GBLA &$PMINC      * IF NESTING LEVEL COUNT                             00130000
 GBLA &$PMPNI      * PROC NESTING INDEX                                 00140000
 GBLA &$PMPNO      * PROC NUMBER WITHIN CASE                            00150000
 GBLA &$PMPSC      * CONDITION (PREDICATE SET) COUNTER                  00160000
 GBLA &$PMNDX      * LABEL SEQUENCE COUNTER                   @JBTR     00170000
.*                                                                      00180000
 GBLB &$PMCCI      * USER DEFINED CASE INDICATOR                        00190000
 GBLB &$PMCEM      * CASE ERROR PROC PRESENT INDICATOR                  00200000
 GBLB &$PMCICS     * CICS OPTION                                        00210000
 GBLB &$PMCIND     * CASE ACTIVE (WITHIN CASE/ECASE) INDICATOR          00220000
 GBLB &$PMDCLS     * DCL ACTIVE (WITHIN DCL/EDCL PAIR)                  00230000
 GBLB &$PMES(20)   * ELSE MACRO PRESENT INDICATOR                       00240000
 GBLB &$PMOAA      * OR AND AND INDICATOR                               00250000
 GBLB &$PMOERR     * CASE SEQ ERR INDICATOR                             00260000
 GBLB &$PMPCI      * PROC CASE INDICATOR                                00270000
 GBLB &$PMPMS      * PROC MAIN ACTIVE SWITCH                            00280000
 GBLB &$PMROS      * RENT (REENTRANT) OPTION SW FROM PROC               00290000
.*                                                                      00300000
 GBLC &$PMCLBL     * CASE LABEL FOR ECASE                               00310000
 GBLC &$PMINS(20)  * IF NESTED SYSNDX VALUE                             00320000
 GBLC &$PMISN(20)  * IF SYMBOLIC NAME                                   00330000
 GBLC &$PMLKR      * PROC LINK REGISTER (PROC PERFORM EPROC)            00340000
 GBLC &$PMPCA(255)  * USER DEFINED CASE ARRAY                           00350000
 GBLC &$PMPMX      * CURRENT SYSNDX VALUE                               00360000
 GBLC &$PMPNM(20)  * PROC NESTED NAME                                   00370000
 GBLC &$PMPNS(20)  * PROC NESTING SYMBOL                                00380000
 GBLC &$PMPTP(20)  * PROC TYPE C=CASE L=CLOSED E=ERROR                  00390000
 GBLC &$PMWR1      * WORK REGISTER 1 NAME                               00400000
 GBLC &$PMWR2      * WORK REGISTER 2 NAME                               00410000
 GBLC &$PMWSN      * WRKAREA SYMBOLIC NAME                              00420000
.*                                                                      00430000
.* -----------------------------------------------------------------    00440000
 LCLA  &I                                                               00450000
 LCLC  &X                                                               00460000
.*                                                                      00470000
         AIF   ('&NAME' NE 'MAIN').NMAIN                                00480000
         AIF   (&$PMCICS).NCICS                                         00490000
&TAG     B     $PMR&$PMPMX                                              00500000
         AGO   .PMCOM                                                   00510000
.NCICS   ANOP                                                           00520000
.* &TAG     DFHPC  TYPE=RETURN                                @JB4      00530000
         AIF   ('&SYSPARM' NE 'TRON').PMCOM                   @JBTR     00540000
DFHEISTG DSECT                                                @JBTR     00550000
TRSAVE1  DS    A                                              @JBTR     00560000
TRADDR1  DS    A                                              @JBTR     00570000
TRADDR2  DS    A                                              @JBTR     00580000
TRDATA   DS    CL8                                            @JBTR     00590000
*                                                             @JBTR     00600000
&TAG     CSECT                                                @JBTR     00610000
         B     G4068723                                       @JBTR     00620000
SAFTRACE ST    14,TRSAVE1                                     @JBTR     00630000
         L     14,DFHEIBP                                     @JBTR     00640000
         ST    14,TRADDR1                                     @JBTR     00650000
         LA    14,TRDATA                                      @JBTR     00660000
         ST    14,TRADDR2                                     @JBTR     00670000
         LA    1,TRADDR1                                      @JBTR     00680000
         L     15,=V(CZFHTR)                                  @JBTR     00690000
         LTR   15,15                                          @JBTR     00700000
         BZ    *+6                                            @JBTR     00710000
         BALR  14,15                                          @JBTR     00720000
         L     14,TRSAVE1                                     @JBTR     00730000
         BR    14                                             @JBTR     00740000
G4068723 DS    0H                                             @JBTR     00750000
.*                                                            @JBTR     00760000
.PMCOM   ANOP                                                           00770000
&$PMPMS  SETB  0                                                        00780000
.*             ---- CHECK FOR MISSING EIF AND EDO                       00790000
         AIF   (&$PMINC EQ 0).MEXIT                                     00800000
         MNOTE 4,'SPM0121 -  &$PMINC MISSING EIF OR EDO MACRO(S).'      00810000
.MEXIT   ANOP                                                           00820000
.*       LTORG                                                @JB4      00830000
         MEXIT                                                          00840000
.*                                                                      00850000
.NMAIN   ANOP                                                           00860000
         AIF   ('&$PMPTP(&$PMPNI)' NE 'L').NCLOSE                       00870000
&X       SETC  '&$PMPNS(&$PMPNI)'   GET SAVE PARM                       00880000
&I       SETA  &$PMPNI             GET NEST NUMBER                      00890000
&$PMPNI   SETA  &$PMPNI-1           DECR NEST NUMBER                    00900000
         AIF   (T'&NAME EQ 'O' OR '&NAME' EQ '&$PMPNM(&I)').CLOSA       00910000
.NOTE120 MNOTE 8,'SPM0120 - NAME SPECIFIED NOT EQUAL TO PROC NAME.'     00920000
.*                                                                      00930000
.CLOSA   ANOP                                                           00940000
         AIF   ('&X'(1,1) NE '(').CLOSC                                 00950000
         AIF   ('&X' NE '(*)').CLOSBA                                   00960000
&TAG     BR    &$PMLKR                                                  00970000
         MEXIT                                                          00980000
.CLOSBA  ANOP                                                           00990000
&TAG     BR    &X                                                       01000000
         MEXIT                                                          01010000
.CLOSC   ANOP                                                           01020000
&TAG     L     &$PMLKR,&X                                               01030000
         BR    &$PMLKR                                                  01040000
.*                                                                      01050000
         AIF   ('&X'(1,3) NE '$PM').CLOSD                               01060000
&X       DC    F'0'                                                     01070000
.CLOSD   ANOP                                                           01080000
*                                                                       01090000
         MEXIT                                                          01100000
.*                                                                      01110000
.NCLOSE  ANOP                                                           01120000
.* EPROC FOR CASE OR ERROR ROUTINES                                     01130000
         AIF   (NOT &$PMPCI).PCERR12                                    01140000
&$PMPCI  SETB  0                                                        01150000
&$PMPNI  SETA  &$PMPNI-1                                                01160000
         B     $PMCX&$PMCNO        * EXIT TO ECASE                      01170000
         MEXIT                                                          01180000
.PCERR12 MNOTE  12,'SPM0112 - NO PROC MACRO ISSUED FOR THIS EPROC'      01190000
         MEND                                                           01200000