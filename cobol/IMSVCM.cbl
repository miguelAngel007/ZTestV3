*     * FO5238 * 06/26/11 PROYECTO REBORN
000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. IMSVCM.
000300*REMARKS.  INTERMEDIATE I/O SUBROUTINE FOR THE ONLINE SERVICE
000400*          CHARGE PARAMETER FILE. OPEN AND CLOSE LOGIC FOR THE
000500*          RELATIONSHIP PRICING SERVICE CHARGE FILE (SCSVC) IS    9916269
000700*          CONTAINED IN THIS PROGRAM.  I/O LOGIC IS HANDLED
000800*          BY SC SYSTEM MODULES.
000900 ENVIRONMENT DIVISION.
001000 DATA DIVISION.
001100 WORKING-STORAGE SECTION.
001200 77  SI-MODULE-INFO      PIC X(64)
001300     VALUE 'IMSVCM    -----TSD-             10/06/98  13.37.13'.
001400*    THIS PROGRAM CONTAINS TRADE SECRETS THAT BELONG
001500*    TO ALLTEL INFORMATION SERVICES, INC. AND IS
001600*    LICENSED BY AN AGREEMENT.  ANY UNAUTHORIZED ACCESS,
001700*    USE, DUPLICATION, OR DISCLOSURE IS UNLAWFUL.
001800*    COPYRIGHT ALLTEL INFORMATION SERVICES, INC.
001900*    1998, ALL RIGHTS RESERVED.
002000 77  PROGRAM-NAME                PIC  X(8)            VALUE
002100                                                      'IMSVCM  '.
002200*
002300 01  HD-I-O-CONTROL-AREA         PIC  X(8).
002400*
002500 01  DUMMY-RECORD                PIC  X.
002600*
002700 01  CHECK-MODULE-PARAMETERS.
002800     05  CHECK-MODULE-PHASE      PIC  X(8).
002900     05  CHECK-MODULE-FLAG       PIC  X.
003000*
003100 01  SC-PHASES.
003200     05  SC-PHASE-SCSVCMV        PIC  X(8)            VALUE       9916269
003300                                                      'SCSVCMV '. 9916269
003600*
003700 01  ERROR-MESSAGES.
003800     05  ERR-MSG-1.
003900         10  FILLER              PIC  X(60)           VALUE
004000     'BCR SC INSTALL FLAG VALUE IS R OR B / SC NOT INSTALLED  '.
004100         10  FILLER              PIC  X(10)           VALUE
004200     '   CTLS = '.
004300         10  ERR-MSG-1-CTLS      PIC  X(8).
004400*
004500     COPY SIWSMESS.
004600*
004700 LINKAGE SECTION.
004800*
004900     COPY SIWSCNTL.
005000*
005100     COPY IMABCRB1.
005200*
005300 01  IM-SC-ACCESS.                                                DWG5464
005305     05  IM-SCSVCM-ACCESS.                                        9916269
005310         10  IM-SCSVCM-FILE-STATUS                                9916269
005315                                 PIC  X.                          DWG5464
005320         10  IM-SCSVCM-CONTROL-ACCESS                             9916269
005325                                 PIC  X.                          DWG5464
005400*
005500     COPY IMWSSVCM.
005600*
005700 PROCEDURE DIVISION           USING  I-O-CONTROL-AREA
005800                                     DDA-BCR-1
005900                                     IM-SC-ACCESS                 DWG5464
006000                                     SC-MASTER.
006100*
006200 MAIN-LINE-ENTER.
006300*
006400     PERFORM I-O-IMSVCMV THRU I-O-IMSVCMV-EXIT.
006500     MOVE I-O-CONTROL-AREA       TO  HD-I-O-CONTROL-AREA.
006600*
006660     IF  BC-SC-INSTALLED EQUAL 'R' OR 'B'                         DWG5464
006670         NEXT SENTENCE                                            DWG5464
006680     ELSE                                                         DWG5464
006690         GO  TO  MAIN-LINE-RETURN.                                DWG5464
006700     IF  IM-SCSVCM-FILE-STATUS EQUAL 'N'                          9916269
006800         GO  TO  MAIN-LINE-RETURN.
006900*
007000 MAIN-LINE-SC-ACCESS.
007100*
007300     IF  I-O-CONTROL-OPERATOR   EQUAL 'E'                         DWG5464
007400         GO  TO  MAIN-LINE-CLOSE-SC-FILES.
007500*
007600     IF  IM-SCSVCM-FILE-STATUS EQUAL 'C'                          9916269
007800         GO  TO  MAIN-LINE-CHECK-MODULES.
007900*
008000     GO  TO  MAIN-LINE-RETURN.
008100*
008200 MAIN-LINE-CHECK-MODULES.
008300*
008310     IF  IM-SCSVCM-FILE-STATUS EQUAL 'C'                          9916269
008400         MOVE SC-PHASE-SCSVCMV   TO  CHECK-MODULE-PHASE           9916269
008500         MOVE ZERO               TO  CHECK-MODULE-FLAG            DWG5464
008600         PERFORM I-O-SICHKMOD THRU I-O-SICHKMOD-EXIT              DWG5464
008700         IF  CHECK-MODULE-FLAG EQUAL ZERO                         DWG5464
008800             GO  TO  MAIN-LINE-MODULE-NOT-FOUND.                  DWG5464
008900*
009600     GO  TO  MAIN-LINE-OPEN-SC-FILES.
009700*
009800 MAIN-LINE-MODULE-NOT-FOUND.
009900*
010000     MOVE PROGRAM-NAME           TO  SIMESS-PROGRAM.
010100     MOVE BC1-CONTROL-KEY        TO  ERR-MSG-1-CTLS.
010200     MOVE 0002                   TO  SIMESS-MESS-NO.
010300     MOVE ERR-MSG-1              TO  SIMESS-OPTIONAL-MESSAGE.
010400     PERFORM I-O-SIMESS THRU I-O-SIMESS-EXIT.
010500     MOVE 'N'                    TO  IM-SCSVCM-FILE-STATUS.       9916269
010600     GO  TO  MAIN-LINE-RETURN.
010700*
010800 MAIN-LINE-OPEN-SC-FILES.
010900*
010910     IF  IM-SCSVCM-FILE-STATUS EQUAL 'C'                          9916269
010920         MOVE 'O'                TO  I-O-CONTROL-OPERATOR         DWG5464
010930         MOVE IM-SCSVCM-CONTROL-ACCESS                            9916269
011000                                 TO  I-O-CONTROL-ACCESS           DWG5464
011200         PERFORM I-O-SCSVCMV THRU I-O-SCSVCMV-EXIT                9916269
011300         MOVE 'O'                TO  IM-SCSVCM-FILE-STATUS.       9916269
011700     GO  TO  MAIN-LINE-RETURN.
011800*
011900 MAIN-LINE-CLOSE-SC-FILES.
012000*
012100     IF  IM-SCSVCM-FILE-STATUS EQUAL 'O'                          9916269
012110         MOVE 'E'                TO  I-O-CONTROL-OPERATOR         DWG5464
012120         MOVE IM-SCSVCM-CONTROL-ACCESS                            9916269
012130                                 TO  I-O-CONTROL-ACCESS           DWG5464
012200         PERFORM I-O-SCSVCMV THRU I-O-SCSVCMV-EXIT                9916269
012300         MOVE 'C'                TO  IM-SCSVCM-FILE-STATUS.       9916269
012800*
012900 MAIN-LINE-RETURN.
013000*
013100     MOVE HD-I-O-CONTROL-AREA    TO  I-O-CONTROL-AREA.
013200     GOBACK.
013300*
013400 I-O-SICHKMOD.
013500     CALL 'SICHKMOD'          USING  CHECK-MODULE-PHASE
013600                                     CHECK-MODULE-FLAG.
013700 I-O-SICHKMOD-EXIT.
013800              EXIT.
013900*
014000 I-O-SIMESS.
014100     CALL 'SIMESS'            USING  SIMESS-AREA.
014200 I-O-SIMESS-EXIT.
014300            EXIT.
014400*
014500 I-O-IMSVCMV.
014600     CALL 'IMSVCMV'           USING  I-O-CONTROL-AREA
014700                                     SC-MASTER.
014800 I-O-IMSVCMV-EXIT.
014900             EXIT.
015000*
015100 I-O-SCSVCMV.                                                     9916269
      * CALL 'SCSVCMV'           USING  I-O-CONTROL-AREA    ORIGINAL    9916269
015200     CALL SC-PHASE-SCSVCMV    USING  I-O-CONTROL-AREA             9916269
015300                                     DUMMY-RECORD.
015400 I-O-SCSVCMV-EXIT.                                                9916269
015500             EXIT.
