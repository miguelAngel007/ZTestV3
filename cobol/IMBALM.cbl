*     * FO5238 * 06/26/11 PROYECTO REBORN
000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    IMBALM.
000300*
000400*--------------------------------------------------------------*
000500*           BALANCE HISTORY INTERMEDIATE I/O ROUTINE           *
000600*--------------------------------------------------------------*
000700*    THIS INTERMEDIATE I/O ROUTINE IS USED TO READ/EXPAND AND  *
000800*    WRITE/COMPRESS THE BALANCE HISTORY RECORDS.               *  0447266
000850*    DUPLICATE BYTES BETWEEN OCCURRENCES ARE COMPRESSED,       *  0447266
000900*    DEPENDING ON THE BCR BALANCE HISTORY COMPRESSION FIELD,   *  0447266
001000*    TO SAVE SPACE ON THE OUTPUT FILE.                         *  0447266
001100*--------------------------------------------------------------*
001200*
001300 ENVIRONMENT    DIVISION.
001400
001500 DATA DIVISION.
001600 WORKING-STORAGE SECTION.
001700 77  SI-MODULE-INFO      PIC X(64)
001800     VALUE 'IMBALM    -----TSD-             05/03/04  14.52.25'.
001900*    THIS PROGRAM CONTAINS TRADE SECRETS THAT BELONG
002000*    TO FIDELITY INFORMATION SERVICES AND IS
002100*    LICENSED BY AN AGREEMENT.  ANY UNAUTHORIZED ACCESS,
002200*    USE, DUPLICATION, OR DISCLOSURE IS UNLAWFUL.
002300*    COPYRIGHT FIDELITY INFORMATION SERVICES
002400*    2004, ALL RIGHTS RESERVED.
002500 01  PROGRAM-NAME                    PIC X(8)    VALUE 'IMBALM'.
002600 01  WORK-FIELDS.
002700     03  W-HOLD-OPERATOR             PIC X(1)    VALUE ' '.
002800     03  W-HOLD-VSAM-KEY             PIC X(23)   VALUE ' '.
002900     03  W-HOLD-ENTRIES              PIC S9999 COMP SYNC VALUE +0.
003000*
003100 01  NON-SUPPORT-MSG.
003200     03  FILLER                      PIC X(36)   VALUE
003300         'NON-SUPPORTED I-O-CONTROL-OPERATOR ('.
003400     03  NON-SUPPORT-LIT             PIC X.
003500     03  FILLER                      PIC X(18)   VALUE
003600         ') PASSED TO MODULE'.
003700*
003800     COPY SIWSMESS.
003900*
003910*    WITH RSEC IM7266, THERE NOW EXITS A FLAG ON THE BCR          0447266
003920*    THAT ALLOWS THE ORGANIZATION TO USE OR NOT USE THE           0447266
003930*    COMPRESSION FEATURE.  WHEN THE FIELD CONTAINS THE            0447266
003940*    VALUE '1', THE BALANCE HISTORY WILL BE PROCESSED AS          0447266
003950*    DESCRIBED BELOW.  WHEN THE VALUE IS '0', THE BALANCE         0447266
003960*    HISTORY IS WRITTEN AND READ WITHOUT ANY COMPRESSION.         0447266
004000*--------------------------------------------------------------*
004100*    THE COMPRESSED BALANCE HISTORY RECORD IS RECEIVED FROM    *
004200*    THE COMPRESS ROUTINE (SIBHCP) WITH DUPLICATE DATA BYTES   *
004300*    BETWEEN OCCURRENCES REPLACED WITH THE CHARACTERS X'FFLL'  *
004400*    (WHERE LL IS THE NUMBER OF DUPLICATE BYTES THAT HAVE BEEN *
004500*    DELETED) AND WRITTEN AS A COMPRESSED VSAM RECORD.  THE    *
004600*    EXPAND ROUTINE (SIBHEP) IS CALLED FOLLOWING A READ OF     *
004700*    EITHER A SEQUENTIAL OR VSAM BALANCE HISTORY RECORD TO     *
004800*    CHANGE THE X'FFLL' ENTRIES IN THE COMPRESSED RECORD BACK  *
004900*    INTO THE DUPLICATE DATA BYTES FOR USE BY THE PROCESSING   *
005000*    PROGRAM.  THE SIZE OF THIS WORKING-STORAGE AREA SHOULD    *
005100*    REFLECT THE SIZE OF THE BALANCE HISTORY RECORD AS DEFINED *
005200*    IN THE IMBALHST COPYBOOK.                                 *
005300*--------------------------------------------------------------*
005400*
005500 01  COMP-BALANCE-HISTORY-REC.
005600     03  CBH-LENGTH                  PIC S9(4)   VALUE +0  COMP.
005700     03  CBH-BIN0                    PIC XX      VALUE ' '.
005800     03  CBH-RECORD.
005900         05  CBH-KEY.
006000             10  CBH-CONTROLS        PIC X(22)   VALUE ' '.
006100             10  CBH-TYPE            PIC X       VALUE ' '.
006110             10  CBH-SUB-TYPE        PIC X       VALUE LOW-VALUES.9916230
006200         05  FILLER                  PIC X       VALUE ' '.       9916230
006300         05  CBH-ENTRIES             PIC S999    COMP-3.
006400         05  CBH-AREA                PIC X(15300) VALUE ' '.
006500     EJECT
006600 LINKAGE SECTION.
006700*
006800     COPY SIWSCNTL.
006900     EJECT
007000     COPY IMBALHST.
007100     EJECT
007200     COPY IMWSENVO.
007300     EJECT
007310     COPY IMWRKBC1.                                               0447266
007320     EJECT                                                        0447266
007400 PROCEDURE DIVISION  USING  I-O-CONTROL-AREA
007500                            BALANCE-HISTORY-REC
007600                            BAL-HIST-LENGTHS
007700                            SI-ENVIRONMENT-AREA                   0447266
007710                            DDA-WRKBCR-1.                         0447266
007800*
007900*--------------------------------------------------------------*
008000*    THE DRIVER PARAGRAPH DETERMINES WHETHER THE INPUT BALANCE *
008100*    HISTORY FILE IS SEQUENTIAL (USED ONLY BY IM31) OR VSAM.   *
008200*--------------------------------------------------------------*
008300*
008400 DRIVER.
008500     MOVE +15331 TO BHL-MAX-RECORD.
008600     IF  SI-88-ENVIRONMENT-IM31
008700         GO TO SQVS-LOGIC
008800     ELSE
008900         GO TO VSAM-LOGIC.
009000*
009100*--------------------------------------------------------------*
009200*    THIS PARAGRAPH SUPPORTS THE I/O FOR A VSAM IN AND VSAM    *
009300*    OUT BALANCE HISTORY FILE.                                 *
009400*--------------------------------------------------------------*
009500*
009600 VSAM-LOGIC.
009700     IF  I-O-88-SEQ-READ
009800         CALL 'IMBALMV' USING I-O-CONTROL-AREA
009900                              COMP-BALANCE-HISTORY-REC
010000         IF  NOT I-O-88-END-OF-FILE
010100             MOVE CBH-KEY TO W-HOLD-VSAM-KEY
010200             GO TO BAL-HIST-EXPAND
010300         ELSE
010400             GO TO GOBACK-PARA.
010500*
010600     IF  I-O-88-REWRITE OR I-O-88-INSERT
010700         GO TO BAL-HIST-COMPRESS.
010800*
010900     IF  I-O-88-RBA-DIRRET
011000         MOVE BH-KEY TO CBH-KEY
011100         CALL 'IMBALMV' USING I-O-CONTROL-AREA
011200                              COMP-BALANCE-HISTORY-REC
011300         IF  BH-KEY EQUAL CBH-KEY
011400             MOVE BH-KEY TO W-HOLD-VSAM-KEY
011500             GO TO BAL-HIST-EXPAND
011600         ELSE
011700             GO TO GOBACK-PARA.
011800*
011900     IF  I-O-88-START-SEQKGE
012000         MOVE BH-KEY TO CBH-KEY
012100         CALL 'IMBALMV' USING I-O-CONTROL-AREA
012200                              COMP-BALANCE-HISTORY-REC
012300         GO TO GOBACK-PARA.
012400*
012500     IF  I-O-88-KEYED-DIRECT
012600         MOVE BH-KEY TO CBH-KEY
012700         CALL 'IMBALMV' USING I-O-CONTROL-AREA
012800                              COMP-BALANCE-HISTORY-REC
012900         IF  NOT I-O-88-NOT-FOUND
013000             MOVE BH-KEY TO W-HOLD-VSAM-KEY
013100             GO TO BAL-HIST-EXPAND
013200         ELSE
013300             GO TO GOBACK-PARA.
013400*
013500     IF  I-O-88-DELETE
013600         CALL 'IMBALMV' USING I-O-CONTROL-AREA
013700                              BALANCE-HISTORY-REC
013800         GO TO GOBACK-PARA.
013900*
014000     IF  I-O-88-REREAD
014100         MOVE W-HOLD-VSAM-KEY TO CBH-KEY
014200         MOVE 'S' TO I-O-CONTROL-OPERATOR
014300         CALL 'IMBALMV' USING I-O-CONTROL-AREA
014400                              COMP-BALANCE-HISTORY-REC
014500         IF  I-O-88-NORMAL-RET
014600             MOVE 'R' TO I-O-CONTROL-OPERATOR
014700             CALL 'IMBALMV' USING I-O-CONTROL-AREA
014800                                  COMP-BALANCE-HISTORY-REC
014900             GO TO BAL-HIST-EXPAND
015000         ELSE
015100             GO TO SIMESS-READ-ERROR.
015200*
015300     IF  I-O-88-OPEN-KSDS OR I-O-88-OPEN-ESDS
015400         CALL 'IMBALMV' USING I-O-CONTROL-AREA
015500                              COMP-BALANCE-HISTORY-REC
015600         GO TO GOBACK-PARA.
015700     IF  I-O-88-CLOSE
015800         CALL 'IMBALMV' USING I-O-CONTROL-AREA
015900                              COMP-BALANCE-HISTORY-REC.
016000     IF  I-O-88-CLOSE
016100         GO TO GOBACK-PARA.
016200     GO TO SIMESS-CTL-ERROR.
016300     EJECT
016400*
016500*--------------------------------------------------------------*
016600*    THIS PARAGRAPH SUPPORTS THE I/O FOR A SEQUENTIAL IN AND   *
016700*    VSAM OUT BALANCE HISTORY FILE.                            *
016800*--------------------------------------------------------------*
016900*
017000 SQVS-LOGIC.
017100     IF  I-O-88-SEQ-READ
017200         CALL 'IMBALMS' USING I-O-CONTROL-AREA
017300                              COMP-BALANCE-HISTORY-REC
017400         IF  NOT I-O-88-END-OF-FILE
017500             GO TO BAL-HIST-EXPAND
017600         ELSE
017700             GO TO GOBACK-PARA.
017800*
017900     IF  I-O-88-LOAD
018000         MOVE 'L' TO I-O-CONTROL-OPERATOR
018100         GO TO BAL-HIST-COMPRESS.
018200*
018300     IF  I-O-88-OPEN-SEQ
018400         MOVE 'O' TO I-O-CONTROL-OPERATOR
018500         MOVE 'I' TO I-O-CONTROL-ACCESS
018600         CALL 'IMBALMS' USING I-O-CONTROL-AREA
018700                              BALANCE-HISTORY-REC
018800         MOVE 'O' TO I-O-CONTROL-OPERATOR
018900         MOVE 'O' TO I-O-CONTROL-ACCESS
019000         CALL 'IMBALMV' USING I-O-CONTROL-AREA
019100                              BALANCE-HISTORY-REC
019200         GO TO GOBACK-PARA.
019300*
019400     IF  I-O-88-CLOSE
019500         MOVE 'I' TO I-O-CONTROL-ACCESS
019600         CALL 'IMBALMS' USING I-O-CONTROL-AREA
019700                              BALANCE-HISTORY-REC
019800         MOVE 'O' TO I-O-CONTROL-ACCESS
019900         CALL 'IMBALMV' USING I-O-CONTROL-AREA
020000                              COMP-BALANCE-HISTORY-REC.
020100
020200     IF  I-O-88-REREAD
020300         CALL 'IMBALMS' USING I-O-CONTROL-AREA
020400                              COMP-BALANCE-HISTORY-REC
020500         GO TO BAL-HIST-EXPAND.
020600
020700     GO TO GOBACK-PARA.
020800*
020900 BAL-HIST-EXPAND.
021000*
021100*    CHECK FOR A HEADER RECORD AND DO NOT EXPAND
021200*
021300     IF  CBH-TYPE EQUAL '0'
021400         MOVE CBH-RECORD      TO BH-RECORD
021500         MOVE CBH-LENGTH      TO BH-LENGTH
021600         MOVE LOW-VALUES      TO BH-BIN0
021700         GO TO GOBACK-PARA.
021710*                                                                 0447266
021720*    CHECK BCR COMPRESSION FLAG AND TAKE APPROPRIATE ACTION       0447266
021730*                                                                 0447266
021740     IF  WBC-BAL-HIST-COMPRESS EQUAL '0'                          0447266
021750         MOVE COMP-BALANCE-HISTORY-REC                            0447266
021760                            TO BALANCE-HISTORY-REC                0447266
021770         GO TO GOBACK-PARA.                                       0447266
021800*
021900*--------------------------------------------------------------*
022000*    MOVE MAXIMUM RECORD LENGTH INTO WORKING STORAGE RECORD.   *
022100*    THIS FIELD IS REQUIRED IN THE FIRST 4 BYTES OF THE FIRST  *
022200*    PARAMETER PASSED TO THE EXPAND ROUTINE.                   *
022300*--------------------------------------------------------------*
022400*
022500     MOVE BHL-MAX-RECORD    TO BH-LENGTH.
022600*
022700*--------------------------------------------------------------*
022800*    CALL THE BALANCE HISTORY EXPAND ROUTINE GIVING:           *
022900*    - BALANCE HISTORY EXPANDED WORKING-STORAGE AREA WITH      *
023000*      MAXIMUM LENGTH OF THE AREA IN THE FIRST 4 BYTES         *
023100*      (SEND THIS LENGTH ONLY AND RECEIVE EXPANDED DATA IN     *
023200*      THIS AREA)                                              *
023300*    - BALANCE HISTORY COMPRESSED WORKING-STORAGE AREA (SEND)  *
023400*--------------------------------------------------------------*
023500*
023600 CALL-EXPAND-ROUTINE.
023700     CALL 'SIBHEP' USING BALANCE-HISTORY-REC
023800                         COMP-BALANCE-HISTORY-REC.
023900*
024000*--------------------------------------------------------------*
024100*    THE EXPAND ROUTINE RETURNS ZERO IN THE LENGTH FIELD IF    *
024200*    THERE IS AN ERROR IN THE PARAMETERS PASSED TO IT.  THE    *
024300*    INPUT RECORD IS MOVED DIRECTLY TO THE OUTPUT RECORD WITH  *
024400*    NO ATTEMPT TO EXPAND IT AND AN ERROR MESSAGE IS ISSUED.   *
024500*--------------------------------------------------------------*
024600*
024700     IF  BH-LENGTH EQUAL ZERO
024800         GO TO SIMESS-EXPAND-ERROR.
024900     GO TO GOBACK-PARA.
025000*
025100 BAL-HIST-COMPRESS.
025200*
025300*    CHECK FOR A HEADER RECORD AND DO NOT COMPRESS
025400*
025500     IF  BH-TYPE EQUAL '0'
025600         CALL 'IMBALMV' USING I-O-CONTROL-AREA
025700                              BALANCE-HISTORY-REC
025800         GO TO GOBACK-PARA.
025810*                                                                 0447266
025820*    CHECK BCR COMPRESSION FLAG AND TAKE APPROPRIATE ACTION       0447266
025830*                                                                 0447266
025840     IF  WBC-BAL-HIST-COMPRESS EQUAL '0'                          0447266
025850         MOVE BALANCE-HISTORY-REC                                 0447266
025860                                 TO COMP-BALANCE-HISTORY-REC      0447266
025870         GO TO WRITE-VSAM-FILE.                                   0447266
025900*
026000*--------------------------------------------------------------*
026100*    CALL THE BALANCE HISTORY COMPRESS ROUTINE GIVING:         *
026200*    - BALANCE HISTORY EXPANDED WORKING-STORAGE WITH THE       *
026300*      ACTUAL LENGTH IN THE FIRST 4 BYTES (SEND)               *
026400*    - BALANCE HISTORY FIXED AREA EXCLUDING RECORD LENGTH      *
026500*      (SEND)                                                  *
026600*    - BALANCE HISTORY ACTUAL NUMBER OF ENTRIES (SEND)         *
026700*    - BALANCE HISTORY COMPRESSED WORKING-STORAGE AREA         *
026800*      (RECEIVE)                                               *
026900*--------------------------------------------------------------*
027000*
027100     MOVE BH-ENTRIES TO W-HOLD-ENTRIES.
027200     CALL 'SIBHCP'  USING BALANCE-HISTORY-REC
027300                          BHL-FIXED
027400                          W-HOLD-ENTRIES
027500                          COMP-BALANCE-HISTORY-REC.
027600 WRITE-VSAM-FILE.
027700     CALL 'IMBALMV' USING I-O-CONTROL-AREA
027800                          COMP-BALANCE-HISTORY-REC.
027900     GO TO GOBACK-PARA.
028000*
028100 SIMESS-READ-ERROR.
028200     MOVE PROGRAM-NAME TO SIMESS-PROGRAM.
028300     MOVE 501          TO SIMESS-MESS-NO.
028400     MOVE 'RE-READ FOR NON-EXISTENT VSAM RECORD' TO
028500         SIMESS-OPTIONAL-MESSAGE.
028600     CALL 'SIMESS' USING SIMESS-AREA.
028700*
028800 SIMESS-CTL-ERROR.
028900     MOVE PROGRAM-NAME TO SIMESS-PROGRAM.
029000     MOVE 501          TO SIMESS-MESS-NO.
029100     MOVE I-O-CONTROL-OPERATOR TO NON-SUPPORT-LIT.
029200     MOVE NON-SUPPORT-MSG TO SIMESS-OPTIONAL-MESSAGE.
029300     CALL 'SIMESS' USING SIMESS-AREA.
029400*
029500 SIMESS-EXPAND-ERROR.
029600     MOVE PROGRAM-NAME TO SIMESS-PROGRAM.
029700     MOVE 501          TO SIMESS-MESS-NO.
029800     MOVE 'BAL HIST EXPAND CANNOT BE PROCESSED ' TO
029900         SIMESS-OPTIONAL-MESSAGE.
030000     CALL 'SIMESS' USING SIMESS-AREA.
030100*
030200 GOBACK-PARA.
030300     GOBACK.
